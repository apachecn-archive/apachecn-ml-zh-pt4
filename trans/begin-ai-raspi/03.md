# 3.专家系统演示

本章包含几个专家系统演示，所有这些都是在 Raspberry 3 独立桌面配置上运行的。第一个演示，Demo 3-1，非常简单——旨在向您展示如何开始在 Raspberry Pi 上使用 Prolog。我已经讨论了如何使用命令行和 GUI 跟踪函数，它们在调试 Prolog 程序时非常有用。第二个演示，Demo 3-2，有点复杂，程序问用户一些关于未知动物的问题，然后试图根据问题的答案得出结论。

下一个专家程序 Demo 3-3 实现了一个井字游戏，复杂性随之增加。我提供了一些关于井字游戏程序如何运行的详细讨论，以便对谓词以及它们如何在 Prolog 程序中使用提供一些见解。下一个演示，演示 3-4，应该有助于诊断你是患了感冒还是流感。这只是一个示范；它不应该代替去看医生。演示 3-5 将 Prolog 专家系统的结果与 Raspberry Pi GPIO 引脚的实际激活相结合。我将向您展示如何安装和使用名为 PySWIP 的库，它允许在 Python 程序中调用和执行 Prolog 命令。

所有这些演示也应该在早期的 Raspberry 模型上运行，但是速度要慢一些。对于演示 3-5，您需要一些额外的零件，这些零件在表 [3-1](#Tab1) 中有所描述。

表 3-1。

Parts Lists

<colgroup><col> <col> <col></colgroup> 
| 描述 | 量 | 评论 |
| --- | --- | --- |
| 皮皮匠 | one | 40 引脚版本，可接受 T 型或 DIP 型 |
| 无焊试验板 | one | 860 个插入点，带电源板 |
| 跳线 | 1 包 |   |
| 发光二极管 | Two |   |
| S7-1200 可编程控制器 | Two | 1/4 瓦特 |

这些器件可从许多在线渠道获得，包括 Adafruit Industries、MCM Electronics、RS Components、Digikey 和 Mouser。

我从一个简单的数据库开始专家系统演示，其中我使用了`trace`命令来说明 Prolog 如何解析用户目标或查询。

## 演示 3-1: Office 数据库

下面的程序和讨论很大程度上是基于 MultiWingSpan 网站上关于用 Prolog 进行跟踪的非常清晰的教程。下面的清单是 Prolog 数据库，它被恰当地命名为 office.pl。

```py
/*office program */
adminWorker(black).
admnWorker(white).

officeJunior(green).

manager(brown).
manager(grey).
supervises(X,Y) :- manager(X), adminWorker(Y).
supervises(X,Y) :- adminWorker(X), officeJunior(Y).
supervises(X,Y) :- manager(X), officeJunior(Y).

```

数据库非常简单:只有关于办公室角色的五个事实和关于谁管理谁的三个规则。图 [3-1](#Fig1) 显示了一个交互式的 Prolog 会话，在该会话中，我向 Prolog 询问了各种办公室成员的角色以及他们的主管。这些查询非常简单，但是没有揭示 Prolog 是如何得出结论的。

![A436848_1_En_3_Fig1_HTML.jpg](A436848_1_En_3_Fig1_HTML.jpg)

图 3-1。

Interactive Prolog session

Prolog 中一个重要的调试工具是`trace`命令。通过跟踪，您可以查看在 Prolog 查询中按顺序执行的所有目标。您还可以查看目标失败时发生的任何“回溯”。此命令会打开跟踪:

```py
?- trace.

Prolog will respond with:

true.

```

完成跟踪后，您可以使用以下命令将其关闭:

```py
?- notrace.

Prolog will respond with:

true.

```

`trace`命令是 SWI Prolog 中实现的 20 多个调试命令之一。涵盖所有使用 Prolog 调试工具的各种方法可能需要单独的一本书。我的目的只是说明一些简单的调试方法，这些方法应该有助于您理解 Prolog 如何与数据库一起工作。

下面是使用我刚才介绍的 office 数据库的命令行跟踪会话。图 [3-2](#Fig2) 是寻人会话的完整截图。

![A436848_1_En_3_Fig2_HTML.jpg](A436848_1_En_3_Fig2_HTML.jpg)

图 3-2。

Office database tracing session

表 [3-2](#Tab2) 是对图 [3-2](#Fig2) 所示跟踪会话的逐行注释。您应该注意到，SWI Prolog 调试器支持六个标准端口，分别是 Call、Exit、Redo、Fail、Exception 和 Unify。您可以在下面的注释中看到其中的一些端口，因为它们代表了 Prolog 解释器对数据库事实和规则所采取的动作。您还会在端口名称后面的括号中看到一个数字。这是数据库中正在处理的当前条款行号。

表 3-2。

Line-by-Line Trace Session Commentary

<colgroup><col> <col></colgroup> 
| 序言对话框/跟踪输出 | 评论 |
| --- | --- |
| `swipl` | 启动 SWI-Prolog |
| `[office].` | 加载 office 数据库。这个表单是 consult 函数的简写。 |
| `trace.` | 开始追踪。 |
| `supervises(Who, green).` | 用户输入查询以确定谁监督员工`green`。 |
| `Call: (6)` `supervises(_G2345, green) ? creep` | Prolog 为 supervisors(X，Y)找到第一条规则，并实例化 Y 以匹配 green，如查询中所述。当您按下 Enter 键时，会出现单词`creep`。这意味着 Prolog 已经被指示移动到下一条指令。Prolog 内存引用 _G2345 用于 X 参数，此后命名为`Who`。 |
| `Call: (7)` `manager(_G2345) ? creep` | Prolog 试图满足规则的第一个子目标。它尝试`manager(X)`。 |
| `Exit: (7)` `manager(brown) ? creep` | `brown`被发现担任经理。Prolog 接下来测试这是否会导致一个解决方案。单词`Exit`反映了这样一个事实，Prolog 已经找到了它最后一次调用的解决方案。它将 X 设置为`brown`。 |
| `Call: (7)` `adminWorker(green) ? creep` | 如果`brown`管理`green`，`green`一定是`adminWorker`。 |
| `Fail: (7)` `adminWorker(green) ? creep` | 因为`green`不是一个`adminWorker`，所以不能满足规则的第二子目标。 |
| `Redo: (7)` `manager(_G2345) ? creep` | Prolog 回溯到第一个子目标，并从它离开`manager(X)`的地方继续。 |
| `Exit: (7)` `manager(grey) ? creep` | Prolog 找到`grey`并将 X 实例化为这个新值。 |
| `Call: (7)` `adminWorker(green) ? creep` | Prolog 再次测试绿色是否是一个`adminWorker`。 |
| `Fail: (7)` `adminWorker(green) ? creep` | 又一次失败了。这意味着该规则无法提供解决方案。 |
| `Redo: (6)` `supervises(_G2345, green) ? creep` | Prolog 回溯到初始规则，并继续处理顶级目标。 |
| `Call: (7)` `adminWorker(_G2345) ? creep` | 这一次，Prolog 找了一个`adminWorker`作为主管。(第二条规则。) |
| `Exit: (7)` `adminWorker(black) ? creep` | Prolog 找到`black`并将 X 实例化为这个新值。 |
| `Call: (7)` `officeJunior(green) ? creep` | Prolog 检查现在是否满足第二子目标。 |
| `Exit: (7)` `officeJunior(green) ? creep` | `green`是`officeJunior`，因此，满足第二子目标。 |
| `Exit: (6)` `supervises(black, green) ? creep` | 顶层目标得到满足。 |

图中的语句`Who = black.`不是跟踪的一部分，而是响应初始查询`supervises(Who, green)`而显示的序言响应。

按下 Enter 键后，所有跟踪线上显示的单词`creep`出现。它只是表明 Prolog 正在进行跟踪的下一行。关于`trace`命令，还有很多额外的信息。只需键入以下内容即可找到有关 trace 及其功能的更多信息:

```py
?- help(trace).

```

您还应该注意到，trace 不适用于 Prolog 的内置函数。你必须依靠 Prolog 丰富的文档来学习这些函数。

除了我刚才演示的命令行版本，SWI Prolog 还提供了一个调试图形用户界面(GUI)。图 [3-3](#Fig3) 显示了在前面的例子中使用相同的数据库和查询来调用 GUI 的命令行会话。

![A436848_1_En_3_Fig3_HTML.jpg](A436848_1_En_3_Fig3_HTML.jpg)

图 3-3。

Invoking the GUI tracer

命令行和 GUI 调用之间的唯一区别是`guitracer`。在 consult 命令后立即输入命令。Prolog 返回以下语句:

```py
% The graphical front-end will be used for subsequent tracing
true.

```

但是，在您实际进入`trace`之前，不会显示任何 GUI。命令，然后输入一个目标，在本例中是`supervises(Who, green)`。从这一点开始，所有的用户跟踪和调试动作都发生在 GUI 对话框屏幕上，如图 [3-4](#Fig4) 所示。

![A436848_1_En_3_Fig4_HTML.jpg](A436848_1_En_3_Fig4_HTML.jpg)

图 3-4。

GUI trace dialog display

您必须反复点击位于工具栏左上角的向右箭头，以逐步完成表 [3-2](#Tab2) 中详述的所有序言操作。图 [3-4](#Fig4) 实际显示了后续步骤的 Prolog 序列状态，摘自表 [3-2](#Tab2) 。

<colgroup><col> <col></colgroup> 
| `Call: (7)` `officeJunior(green) ? creep` | Prolog 检查现在是否满足第二子目标。 |

调用堆栈的图形表示也显示在 GUI 显示的右上方窗格中。与命令行版本相比，许多 Prolog 用户更喜欢 GUI 表示，但是您可以放心，这两个版本执行的跟踪操作完全相同。

您可以使用类似于命令行版本的命令来停止 GUI 跟踪:

```py
?- noguitracer.

```

下一个专家系统演示是一个经典游戏，呈现给大多数初学人工智能的学生。

## 演示 3-2:动物识别

这个专家系统是一个动物识别游戏，它是最初出现在 Barr、Cohen 和 Feigenbaum 编辑的《人工智能手册》第 4 卷(Addison-Wesley，1990)中的 Lisp 程序的 Prolog 版本。这是一个相对简单的程序，它试图从七个选项中识别出你正在考虑的动物:

*   猎豹
*   老虎
*   长颈鹿
*   斑马
*   鸵鸟
*   企鹅
*   信天翁

该程序被设置为询问一系列问题，以努力确定动物。我建议你先试试这个程序，然后我再讨论它是如何工作的。你只需要输入一个`yes`或`no`来回答问题。你的回答甚至可以缩短为`y`或`n`。输入以下内容

```py
?- go.

```

加载程序后。

下面是 Prolog 动物脚本的清单。

```py
/* animal.pl
  animal identification game.  

    start with ?- go.     */

go :- hypothesize(Animal),
      write('I guess that the animal is: '),
      write(Animal),
      nl,
      undo.

/* hypotheses to be tested */
hypothesize(cheetah)   :- cheetah, !.
hypothesize(tiger)     :- tiger, !.
hypothesize(giraffe)   :- giraffe, !.
hypothesize(zebra)     :- zebra, !.
hypothesize(ostrich)   :- ostrich, !.
hypothesize(penguin)   :- penguin, !.
hypothesize(albatross) :- albatross, !.
hypothesize(unknown).             /* no diagnosis */

/* animal identification rules */
cheetah :- mammal,
           carnivore,
           verify(has_tawny_color),
           verify(has_dark_spots).
tiger :- mammal,  
         carnivore,
         verify(has_tawny_color),
         verify(has_black_stripes).
giraffe :- ungulate,
           verify(has_long_neck),
           verify(has_long_legs).
zebra :- ungulate,  
         verify(has_black_stripes).

ostrich :- bird,  
           verify(does_not_fly),
           verify(has_long_neck).
penguin :- bird,
           verify(does_not_fly),
           verify(swims),
           verify(is_black_and_white).
albatross :- bird,
             verify(appears_in_story_Ancient_Mariner),
             verify(flys_well).

/* classification rules */
mammal    :- verify(has_hair), !.
mammal    :- verify(gives_milk).
bird      :- verify(has_feathers), !.
bird      :- verify(flys),
             verify(lays_eggs).
carnivore :- verify(eats_meat), !.
carnivore :- verify(has_pointed_teeth),
             verify(has_claws),
             verify(has_forward_eyes).
ungulate :- mammal,
            verify(has_hooves), !.
ungulate :- mammal,
            verify(chews_cud).

/* how to ask questions */
ask(Question) :-
    write('Does the animal have the following attribute: '),
    write(Question),
    write('? '),
    read(Response),
    nl,
    ( (Response == yes ; Response == y)
      ->
       assert(yes(Question)) ;
       assert(no(Question)), fail).

:- dynamic yes/1,no/1.

/* How to verify something */
verify(S) :-
   (yes(S)
    ->
    true ;
    (no(S)
     ->
     fail ;
     ask(S))).

/* undo all yes/no assertions */
undo :- retract(yes(_)),fail.
undo :- retract(no(_)),fail.
undo.

```

这个程序很有趣，因为它试图验证属性以得出结论。问题的答案也会被简单地储存起来以供将来参考。当用`yes`提问并回答问题时，则通过断言子句`yes(question)`并成功来记录答案；否则，通过断言子句`no(question)`和失败来记录答案。记录`yes`答案是因为稍后`no`在试图验证相同假设时对不同问题的回答可能导致整个假设失败；而相同的`yes`答案可能会在稍后的过程中成功验证不同的假设。记录答案是程序避免两次问同一个问题的方式。通过检查`yes(question)`是否在存储器中并已成功，或者`no(question)`是否已存储并已失败，来验证问题中指定的条件。如果两个检查都不为真，则`ask(question)`完成。

图 [3-5](#Fig5) 显示了这个程序的一个示例交互会话，其中我进行了几次问答。

![A436848_1_En_3_Fig5_HTML.jpg](A436848_1_En_3_Fig5_HTML.jpg)

图 3-5。

Interactive animal program session

我确定这个程序会得出一个关于食肉动物的错误结论，因为它增加了一条规则，只根据尖牙、利爪和向前看的眼睛对食肉动物进行分类。图 [3-6](#Fig6) 显示了我回答`no`动物是否吃肉的问题和`yes`关于牙齿、爪子和眼睛的问题的互动环节。

![A436848_1_En_3_Fig6_HTML.jpg](A436848_1_En_3_Fig6_HTML.jpg)

图 3-6。

Incorrect conclusion animal session

在这个特殊的专家系统中，这种行为只是指出，如果规则与它们所基于的真实世界的模型不一致，就会得到不正确的结果。从定义上来说，食肉动物就是肉食者，尽管我故意对这个问题回答了不。在接下来的演示中，我将从大型猫科动物和鸟类转移到一个更友好、更有趣的专家系统。

## 演示 3-3: tic-tac-toe

井字游戏，在美国被称为井字游戏，在其他国家被称为十字游戏，是一种非常有趣的游戏，经常被非常小的孩子玩得非常有效。它也可以由专家系统来实现。下面的列表是一个简单的名为 tictactoe.pl 的井字游戏程序，可以通过输入以下内容在计算机上运行:

```py
?- playo.

```

还有一个自玩选项，电脑和自己玩。那个选项总是以 X 赢结束。要开始自我游戏，请输入以下内容:

```py
?- selfgame.

The tictactoe.pl listing follows:

% A tic-tac-toe program in Prolog.   S. Tanimoto, May 11, 2003.
% Additional comments   D. J. Norris, Jan, 2017.
% To play a game with the computer, type
% playo.
% To watch the computer play a game with itself, type
% selfgame.

% Predicates that define the winning conditions:

win(Board, Player) :- rowwin(Board, Player).
win(Board, Player) :- colwin(Board, Player).
win(Board, Player) :- diagwin(Board, Player).

rowwin(Board, Player) :- Board = [Player,Player,Player,_,_,_,_,_,_].
rowwin(Board, Player) :- Board = [_,_,_,Player,Player,Player,_,_,_].
rowwin(Board, Player) :- Board = [_,_,_,_,_,_,Player,Player,Player].

colwin(Board, Player) :- Board = [Player,_,_,Player,_,_,Player,_,_].
colwin(Board, Player) :- Board = [_,Player,_,_,Player,_,_,Player,_].
colwin(Board, Player) :- Board = [_,_,Player,_,_,Player,_,_,Player].

diagwin(Board, Player) :- Board = [Player,_,_,_,Player,_,_,_,Player].
diagwin(Board, Player) :- Board = [_,_,Player,_,Player,_,Player,_,_].

% Helping predicate for alternating play in a "self" game:

other(x,o).
other(o,x).

game(Board, Player) :- win(Board, Player), !, write([player, Player, wins]).
game(Board, Player) :-
  other(Player,Otherplayer),
  move(Board,Player,Newboard),
  !,
  display(Newboard),
  game(Newboard,Otherplayer).

% These move predicates control how a move is made

move([b,B,C,D,E,F,G,H,I], Player, [Player,B,C,D,E,F,G,H,I]).
move([A,b,C,D,E,F,G,H,I], Player, [A,Player,C,D,E,F,G,H,I]).
move([A,B,b,D,E,F,G,H,I], Player, [A,B,Player,D,E,F,G,H,I]).
move([A,B,C,b,E,F,G,H,I], Player, [A,B,C,Player,E,F,G,H,I]).
move([A,B,C,D,b,F,G,H,I], Player, [A,B,C,D,Player,F,G,H,I]).
move([A,B,C,D,E,b,G,H,I], Player, [A,B,C,D,E,Player,G,H,I]).
move([A,B,C,D,E,F,b,H,I], Player, [A,B,C,D,E,F,Player,H,I]).
move([A,B,C,D,E,F,G,b,I], Player, [A,B,C,D,E,F,G,Player,I]).
move([A,B,C,D,E,F,G,H,b], Player, [A,B,C,D,E,F,G,H,Player]).

display([A,B,C,D,E,F,G,H,I]) :- write([A,B,C]),nl,write([D,E,F]),nl,
 write([G,H,I]),nl,nl.

selfgame :- game([b,b,b,b,b,b,b,b,b],x).

% Predicates to support playing a game with the user:

x_can_win_in_one(Board) :- move(Board, x, Newboard), win(Newboard, x).

% The predicate orespond generates the computer's (playing o) reponse
% from the current Board.

orespond(Board,Newboard) :-
  move(Board, o, Newboard),
  win(Newboard, o),
  !.
orespond(Board,Newboard) :-
  move(Board, o, Newboard),
  not(x_can_win_in_one(Newboard)).
orespond(Board,Newboard) :-
  move(Board, o, Newboard).
orespond(Board,Newboard) :-
  not(member(b,Board)),
  !,
  write('Cats game!'), nl,
  Newboard = Board.

% The following translates from an integer description
% of x's move to a board transformation.

xmove([b,B,C,D,E,F,G,H,I], 1, [x,B,C,D,E,F,G,H,I]).
xmove([A,b,C,D,E,F,G,H,I], 2, [A,x,C,D,E,F,G,H,I]).
xmove([A,B,b,D,E,F,G,H,I], 3, [A,B,x,D,E,F,G,H,I]).
xmove([A,B,C,b,E,F,G,H,I], 4, [A,B,C,x,E,F,G,H,I]).
xmove([A,B,C,D,b,F,G,H,I], 5, [A,B,C,D,x,F,G,H,I]).
xmove([A,B,C,D,E,b,G,H,I], 6, [A,B,C,D,E,x,G,H,I]).
xmove([A,B,C,D,E,F,b,H,I], 7, [A,B,C,D,E,F,x,H,I]).
xmove([A,B,C,D,E,F,G,b,I], 8, [A,B,C,D,E,F,G,x,I]).
xmove([A,B,C,D,E,F,G,H,b], 9, [A,B,C,D,E,F,G,H,x]).
xmove(Board, N, Board) :- write('Illegal move.'), nl.

% The 0-place predicate playo starts a game with the user.

playo :- explain, playfrom([b,b,b,b,b,b,b,b,b]).

explain :-
  write('You play X by entering integer positions followed by a period.'),
  nl,
  display([1,2,3,4,5,6,7,8,9]).

playfrom(Board) :- win(Board, x), write('You win!').
playfrom(Board) :- win(Board, o), write('I win!').
playfrom(Board) :- read(N),
  xmove(Board, N, Newboard),
  display(Newboard),
  orespond(Newboard, Newnewboard),
  display(Newnewboard),
  playfrom(Newnewboard).

```

图 [3-7](#Fig7) 显示了我对着电脑玩的一个游戏。

![A436848_1_En_3_Fig7_HTML.jpg](A436848_1_En_3_Fig7_HTML.jpg)

图 3-7。

Game played against the computer

您应该注意到程序在回合结束时显示了`Cats game!`，这是平局游戏的井字游戏术语。

我还发起了一个自我游戏，在这个游戏中，计算机与自己进行对抗。图 [3-8](#Fig8) 显示了该结果。它总是以 X 获胜而结束，正如我前面提到的。

![A436848_1_En_3_Fig8_HTML.jpg](A436848_1_En_3_Fig8_HTML.jpg)

图 3-8。

Self-game

至此，我将讨论井字游戏程序的内部工作原理，现在您已经看到了它是如何运行的。定义获胜有三个规则或谓词:按行、按列或按对角线。下面是一个通用的 win 谓词:

```py
win(Board, Player) :- rowwin(Board, Player).

```

接下来，有三种获胜方式:按行或按列，两种按对角线。下面的谓词是完成顶行的一种方法:

```py
rowwin(Board, Player) :- Board = [Player,Player,Player,_,_,_,_,_,_].

```

对于其他行、列和对角线，也生成了类似的谓词，您可以通过查看代码看到这一点。

有九个移动谓词控制如何移动，对应于九个棋盘位置中的每一个。

如下所述，谓词控制人类玩家如何与游戏交互:

```py
x_can_win_in_one(Board) :- move(Board, x, Newboard), win(Newboard, x).

```

类似地，一系列的`orespond`谓词控制计算机如何与游戏交互。

最后，有九个名为`xmove`的谓词确保只能进行合法的移动。它们还将内部游戏位置表示从 A、B、C…转换为相应的显示位置 1、2、3…

下一个专家系统演示将处理一个我们偶尔会遇到的情况:确定我们是患了感冒还是流感。

## 演示 3-4:感冒或流感诊断

这个例子是一个非常基本的医疗诊断专家系统，在这个系统中，你回答几个问题，系统就试图判断你是患了流感还是更温和的感冒。

Caution

这个专家系统绝不能代替真正的医生建议和咨询。如果你真的病了，请去看医生。不要依赖此程序进行可信的诊断。

下面这个程序命名为 flu_cold.pl。

```py
% flu_cold.pl
% Flu or cold identification example
% Start with ?- go.

go:- hypothesis(Disease),
    write('I believe you have: '),
    write(Disease),
    nl,
    undo.

% Hypothesis to be tested
hypothesis(cold):- cold, !.
hypothesis(flu):- flu, !.

% Hypothesis Identification Rules
cold :-
       verify(headache),
       verify(runny_nose),
       verify(sneezing),
       verify(sore_throat).
flu :-
       verify(fever),
       verify(headache),
       verify(chills),
       verify(body_ache).

% Ask a question
ask(Question) :-
    write('Do you have the following symptom: '),
    write(Question),
    write('? '),
    read(Response),
    nl,
    ( (Response == yes ; Response == y)
      ->
       assert(yes(Question)) ;
       assert(no(Question)), fail).

:- dynamic yes/1,no/1.

% Verify something
verify(S) :- (yes(S) -> true ;
               (no(S)  -> fail ;
               ask(S))).

% Undo all yes/no assertions
undo :- retract(yes(_)),fail.
undo :- retract(no(_)),fail.
undo.

```

正如注释中所提到的，您可以这样启动程序:

```py
?- go.

```

症状会一个接一个地出现。只需用`yes.`或`no.`回答即可(或者，您可以使用单个字母`y.`或`n.`)。不要忘记在回答的末尾输入句号；否则，Prolog 不会识别您的输入。如果您选择的症状组合不包含任何一个假设，那么 Prolog 将简单地显示失败，因为它无法将您的输入与已知的事实相匹配。

图 [3-9](#Fig9) 显示了我用这个专家系统进行的一个示例会话。我选择了流感、感冒的症状，最后是一些不匹配病例的症状。

![A436848_1_En_3_Fig9_HTML.jpg](A436848_1_En_3_Fig9_HTML.jpg)

图 3-9。

Cold and flu expert system session

下一个演示是本章的最后一个。它涉及到另一个简单的专家系统——但有所不同，因为该系统控制几个 Raspberry Pi GPIO 输出。

## 演示 3-5:带有 Raspberry Pi GPIO 控件的专家系统

到目前为止，我已经向您展示了可以在任何装有 Prolog 兼容版本的 PC 或 Mac 上运行的专家系统。本演示与众不同，因为我向您展示了如何使用 Prolog 直接控制一些通用输入/输出(GPIO)引脚，这在普通 PC 上通常是不可能的。

老实说，使用 Prolog 命令直接控制 GPIO 引脚是不可能的；但是如果你把 Prolog 和 Python 语言结合起来，这是可能的。这种结合是由一个名为 PySWIP 的伟大程序实现的，它允许在 Python 程序中调用和执行 Prolog 命令。还有一个很棒的 Python 应用程序编程接口(API)，名为 RPi。GPIO，方便 GPIO 引脚控制。接下来，我将描述如何安装 PySWIP 应用程序并设置 RPi。GPIO API，这两者都是这个专家系统演示的先决条件。

### 安装 PySWIP

PySWIP 由 Yuce Tekol 创建，作为 Prolog 和 Python 之间的桥梁程序。他将它作为 GPL 开源软件提供给社区。该程序不是 Raspian 库的一部分，因此不能使用 apt 包管理器安装。要安装它，您必须使用 pip 程序。如果您的 Raspberry Pi 上没有安装 pip，请使用以下命令进行安装:

```py
sudo apt-get install python-pip

```

一旦安装了 pip，您就可以用它来安装 PySWIP，命令如下:

```py
sudo pip install pyswip

```

为了让 Python 识别 Prolog，还必须完成一个额外的步骤，那就是在原始的共享库名和最新的共享库名之间创建一个符号链接。只需输入以下命令来创建链接:

```py
sudo ln -s libswipl.so /usr/lib/libpl.so

```

接下来，您应该在交互式 Python 会话中输入以下 Python 命令来测试 PySWIP 安装是否成功:

```py
>>> from pyswip import Prolog
>>> prolog = Prolog()
>>> prolog.assertz("father(michael,john)")
>>> prolog.assertz("father(michael,gina)")
>>> list(prolog.query("father(michael,X)"))

```

下面一行是对前面命令的 Prolog 响应:

```py
[{'X': 'john'}, {'X': 'gina'}]
>>> for soln in prolog.query("father(X,Y)"):

```

确保下一行缩进。我用了四个空格。

```py
...     print soln["X"], "is the father of", soln["Y"]
...

```

按退格键和回车键执行`for`语句。Python 解释器应该显示以下内容:

```py
michael is the father of john
michael is the father of gina

```

如果您看到这两行，您可以确信 Python 和 Prolog 能够很好地协同工作，这要感谢 PySWIP 桥程序。图 [3-10](#Fig10) 显示了使用覆盆子 Pi 进行的测试。

![A436848_1_En_3_Fig10_HTML.jpg](A436848_1_En_3_Fig10_HTML.jpg)

图 3-10。

Prolog and Python compatibility test

现在是讨论硬件设置的时候了，它使用 Python/Prolog 专家系统。

### 硬件设置

我使用了一个 T 形 Pi 补鞋匠附件来扩展 Raspberry Pi GPIO 引脚，以便在此设置中可以轻松地与无焊试验板一起使用。图 [3-11](#Fig11) 是烧结图，显示了两个发光二极管和两个限流电阻的设置。

![A436848_1_En_3_Fig11_HTML.jpg](A436848_1_En_3_Fig11_HTML.jpg)

图 3-11。

Fritzing diagram

led 连接到 GPIO 引脚#4 和#17，220ω限流电阻接地。因此，当引脚设置为高值时，led 亮起，对于 Raspberry Pi 为 3.3V。LED 串联电阻将最大电流设置为约 12 ma，这完全在任何给定 Raspberry Pi GPIO 引脚的 25 ma 电流限值范围内。

图 [3-12](#Fig12) 显示了带有 T Pi 补鞋匠、无焊试验板、led 和其他组件的 Raspberry Pi 物理设置。

![A436848_1_En_3_Fig12_HTML.jpg](A436848_1_En_3_Fig12_HTML.jpg)

图 3-12。

Raspberry Pi physical setup

接下来您应该设置 RPi。用 Python 控制 led 的 GPIO API。

### Rpi。GPIO 设置

这个讨论集中在 RPi。GPIO API 用于 Python 程序中。这些设置步骤必须包含在每个需要控制 GPIO 引脚的 Python 程序中。我在一个交互式会话中演示了该设置，但是您应该意识到，相同的语句必须包含在常规的程序脚本中。

RPi。GPIO API 现在包含在所有标准的 Raspian Linux 发行版中。如果您使用的是最新的 Jessie Raspian 发行版，您应该已经准备好了。第一步是导入 API，如下所示:

```py
>>> import RPi.GPIO as GPIO

```

从这一点开始，所有对 API 的引用都使用 GPIO 名称。接下来，您必须选择适当的管脚编号方案。Raspberry Pi 引脚编号方案有两种变体:

*   GPIO。BOARD:P1 接头上物理管脚号后面的板号，它包含所有的 GPIO 管脚。
*   GPIO。BCM:芯片制造商 BROADCOM 或 BCM 使用的编号方案。

圆周率补鞋匠的引脚号码遵循 BCM 方案，所以这是我用的。您可以建立与此语句一起使用的编号方案:

```py
>>> GPIO.setmode(GPIO.BCM)

```

接下来，必须将两个选定的引脚从默认输入模式设置为输出模式，这需要使用以下两条语句来完成:

```py
>>> GPIO.setup(4, GPIO.OUT)
>>> GPIO.setup(17, GPIO.OUT)

```

现在，一切都应该建立切换引脚高或低。默认情况下，它们在启动时被设置为低电平状态。下面两条语句应该会打开两个 led:

```py
>>> GPIO.output(4, GPIO.HIGH)
>>> GPIO.output(17, GPIO.HIGH)

```

如果指示灯没有亮起，您应该再次检查试验板中指示灯的方向。每个标准 LED 都有两条“腿”，其中一条比另一条稍短。较短的脚连接到电阻器的一个引线，电阻器的另一个引线接地。尝试切换 LED 的方向，看看它是否打开。转换方向不会损坏 LED。此外，再次检查您是否连接到引脚 4 和 17，以及您是否选择了正确的编号方案。我发现，当某些东西不能按预期工作时，通常是连接问题或简单的设置错误。

使用以下语句可以关闭 led:

```py
>>> GPIO.output(4, GPIO.LOW)
>>> GPIO.output(17, GPIO.LOW)

```

现在可以创建一个专家系统来控制发光二极管。

### LED 控制专家系统

我决定基本上使用用于验证 PySWIP 安装的相同的简单 Prolog 脚本。然而，该程序进行了一点重组，以利用 Python 使用函数的方式。这就是在程序开始附近使用 PySWIP 仿函数的原因。该程序名为 LEDtest.py，通过输入以下内容运行:

```py
python LEDtest.py

```

下面是 LEDtest.py 清单。

```py
# LEDtest.py by D. J. Norris  Jan, 2017
# Uses Prolog with Python type functions

import time
import RPi.GPIO as GPIO
from pyswip import Functor, Variable, Query, call

# Setup GPIO pins
GPIO.setmode(GPIO.BCM)
GPIO.setup(4, GPIO.output)
GPIO.setup(17, GPIO.output)

# Setup Python like functions for Prolog statements
assertz = Functor("assertz", 1)
father = Functor("father", 2)

# Add facts to a dynamic database
call(assertz(father("michael","john")))
call(assertz(father("michael", "gina")))

# Setup an iterative query session
X = Variable()
q = Query(father("michael",X))
while q.nextSolution():
    print "Hello, ", X.value
    if str(X.value) == "john": # LED #4 on if john is michael's child
        GPIO.output(4,GPIO.HIGH)
        time.sleep(5)
        GPIO.output(4,GPIO.LOW)
    if str(X.value) == "gina": # LED #17 on if gina is michael's child
        GPIO.output(17,GPIO.HIGH)
        time.sleep(5)
        GPIO.output(17,GPIO.LOW)

```

图 [3-13](#Fig13) 显示了程序输出。无法显示的是，每个 led 都亮了五秒钟，这表示两个查询成功运行。

![A436848_1_En_3_Fig13_HTML.jpg](A436848_1_En_3_Fig13_HTML.jpg)

图 3-13。

Program output

在这一点上，您可能想知道如何用 Raspberry Pi 将这个演示付诸实践。这个答案将不得不推迟几章，直到我介绍一个模糊逻辑项目，该项目使用一个嵌入式专家系统和一些 GPIO 引脚来控制一个加热和冷却系统(HVAC)。

## 摘要

本章展示了五个专家系统。它们从只有几个事实和规则的极其简单的游戏，到实现井字游戏的复杂得多的游戏。

最后一个演示向您展示了如何将 Python 与 Prolog 相结合，以便可以控制 Raspberry Pi GPIO 引脚。