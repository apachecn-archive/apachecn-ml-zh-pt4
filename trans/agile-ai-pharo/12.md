# 12.建造动物形态的生物

遗传算法通常用于模拟生物个体行为的各个方面。这一章是关于人工生命的。它定义并创造了我们称之为*的动物形态生物*。我们把动物形态的生物称为虚拟生物，它们拥有生物生物的特殊特征。因此，一个动物形态的生物可以被认为是一个小的数字动物。

![../images/484203_1_En_12_Chapter/484203_1_En_12_Fig1_HTML.jpg](../images/484203_1_En_12_Chapter/484203_1_En_12_Fig1_HTML.jpg)

图 12-1

一种生物的例子

图 [12-1](#Fig1) 展示了这种生物站在平台上的例子。生物是由连接点和肌肉组成的。每块肌肉都有两个末端，每个末端都连接到一个连接点。我们的生物是无骨的，连接点连接肌肉。连接点是肌肉末端的宿主。

肌肉是我们模型中的一个复杂元素。每块肌肉都会振动，并具有一定的强度，这使得它能够抵抗外力(例如，重力或来自平台的反作用力)。肌肉振动是由每块肌肉特有的生物钟调节的。一个生物受到(I)重力和(ii)来自该生物站立的平台的反作用力。肌肉没有重量，但是连接点有重量。

一个生物，在它开始的时候，不能做太多。然而，我们将使它进化以解决一个特殊的任务。我们要考虑的任务是朝着一个方向前进。观察任何一个蹒跚学步的孩子都有助于我们记住走路有多复杂，学习有多困难。

这一章展示了构建一个复杂模拟的基础设施，动物形态的生物将在其中生活。请注意，这一章与遗传算法没有直接关系，因为它只提供了使生物行为的必要基础设施。特别是，它涵盖了一个简单的物理引擎的实现，以模拟一个物理环境。这一章是关于建造动物形态的生物，下一章是关于进化这样的生物，让它们可以四处移动。

## 12.1 建模连接点

我们的模拟中的每个元素都有一个可视化的表示。因此，我们将定义`CVisualElement`类，它将是我们在本章中构建的类层次结构的根:

```
Object subclass: #CVisualElement
    instanceVariableNames: 'element'
    classVariableNames: ''
    package: 'Creature'

```

`CVisualElement`类有一个`element`变量，它将包含一个 Roassal 可视元素。生物和平台的视觉效果来自于专门的漫游元素。每个可视元素都必须通过覆盖`createElement`方法在`CVisualElement`的子类中创建，如下所示:

```
CVisualElement>>createElement
    "Should be overridden in subclasses.
    The method initializes the element variable"
    self subclassResponsibility

```

Roassal 元素可通过以下方式访问:

```
CVisualElement>>element
    "Return the Roassal element"
    ^ element

```

连接点是几块肌肉的连接点。连接点被表示为`CNode`类的实例，定义如下:

```
CVisualElement subclass: #CNode
    instanceVariableNames: 'speedVector force isOnPlatform'
    classVariableNames: ''
    package: 'Creature'

```

一个节点有三个变量:

*   `force`代表外力的总和。这个向量影响速度，从而影响节点的移动。

*   `speedVector`代表节点的速度。速度受到环境引起的摩擦力的影响。

*   `isOnPlatform`表示节点是否在平台上。了解这一点很重要，因为这可能会与平台产生摩擦。

节点的初始化定义如下:

```
CNode>>initialize
    super initialize.
    isOnPlatform := false.
    self resetSpeed.
    self resetForce

```

创建时，节点没有速度，不在平台上，也没有外力对其施加。可以使用以下方法重置外力:

```
CNode>>resetForce
    "Reset the force exercising on the node"
    force := 0 @ 0

```

同样，使用以下方法重置速度:

```
CNode>>resetSpeed
    "Make the node stop by canceling its speed"
    speedVector := 0 @ 0

```

我们决定用一个灰色的小圆圈来表示一个节点。因此，我们覆盖了在`CVisualElement`类中定义的`createElement`方法，以给出节点的可视化方面:

```
CNode>>createElement
    element := RTEllipse new size: 10;
                color: Color gray trans; element.
    element @ RTDraggable

```

一个节点受到外力作用。我们定义了`addForce:`方法，该方法简单地将一个力(表示为一个点)添加到节点上已经施加的力上。该方法如下:

```
CNode>>addForce: aForceAsPoint
    "Make the node subject of an external force"
    force := force + aForceAsPoint

```

总的来说，我们的模拟是由一个节拍驱动的，这是全局触发的，正如我们稍后在对物理世界建模时将看到的那样。一拍对应于一个离散的时间单位，`beat`方法定义如下:

```
CNode>>beat
    "Make the node act according to the force and speed applied to the node"
    speedVector := (speedVector + self gravityForce + force) * 0.9.
    isOnPlatform ifTrue: [
        speedVector := speedVector x * 0.3 @ speedVector y ].
    self translateBy: speedVector

```

方法是我们物理引擎的核心。每拍一次，重力和外力加起来就是速度。我们任意设置一个摩擦力，这是空气摩擦力对物理环境的影响。这种与空气的摩擦，用值`0.9`表示，应用于每一拍。如果节点与平台接触，`speedVector`的`X`分量减少 70%(即乘以`0.3`)。`beat`方法通过将节点平移`speedVector`的计算量来结束。重力由任意一点表示:

```
CNode>>gravityForce
    "A fixed force representing a gravity"
    ^ 0 @ 0.3

```

生物将生活在一个由平台构成的世界里。平台是`CPlatform`类的一个实例，我们将在后面看到。然而，节点必须对与平台的冲突做出响应。我们将`CNode`类上的`checkForCollision:`方法定义如下:

```
CNode>>checkForCollision: platforms
    "Verify if the node is on a platform. If it is the case,
    the variable isOnPlatform is set to true"
    isOnPlatform := false.
    platforms
        do: [ :p |
            (p collide: self)
                ifTrue: [
                    speedVector := speedVector x @ 0.
                    p adjustNodeIfNecessary: self.
                    isOnPlatform := true.
                    ^ self ] ]

```

首先将`isOnPlatform`变量设置为`false`。如果节点与至少一个平台碰撞，那么速度的 Y 分量被设置为`0`，变量`isOnPlatform`被设置为`true`。由于我们模型的一些不精确，我们需要让平台对节点进行一些调整。特别是，它确保节点不会位于平台内*，因为节点移动是离散增量而非连续增量的结果。可以使用以下访问器来访问`isOnPlatform`变量:*

```
CNode>>isOnPlatform
    "Is the node on a platform?"
    ^ isOnPlatform

```

节点的位置由`position`方法给出。它只是简单地向 Roassal `element`询问它的位置。一个新创建的节点位于`0@0`位置。访问节点位置简单定义如下:

```
Node>>position
    "Return the position of the node"
    ^ element position

```

一个节点需要被转换以反映环境的影响。我们定义了一种方法，通过增量步骤平移节点，如下所示:

```
CNode>>translateBy: aPoint
    "Translate the node by an incremental point"
    element translateBy: aPoint.

```

可以使用以下方法为节点设置新位置:

```
CNode>>translateTo: aPoint
    "Translate the node to a new position"
    element translateTo: aPoint.

```

最后一个方法总结了节点的定义。如您所见，许多操作，如翻译和维护节点位置，都委托给了 Roassal 可视化引擎。

## 12.2 建模平台

除了我们之前描述的重力之外，环境可能会影响带有平台的节点(以及生物)。我们将`CPlatform`类定义为`CVisualElement`的子类:

```
CVisualElement subclass: #CPlatform
    instanceVariableNames: 'width height'
    classVariableNames: ''
    package: 'Creature'

```

平台被定义为一个可视的矩形，有一个`width`和`height`组件。我们初始化一个平台，默认宽度为`100`像素，默认高度为`10`像素:

```
CPlatform>>initialize
    super initialize.
    self width: 100.
    self height: 10

```

像往常一样，我们需要专门的方法来改变这些变量的值。平台的宽度通过以下方式设定:

```
CPlatform>>width: aWidthAsNumber
    "Set the width of the platform"
    width := aWidthAsNumber

```

平台的高度通过以下方式设定:

```
CPlatform>>height: aHeightAsNumber
    "Set the height of the platform"
    height := aHeightAsNumber

```

平台在视觉上表示为灰色矩形。必须充分定义`createElement`方法:

```
CPlatform>>createElement
    "Create the visual representation of a platform"
    element ifNotNil: [ "already created" ^ self ].
    element := RTBox new width: width; height: height; color: Color gray; element.

```

可使用以下方法将平台平移至特定位置:

```
CPlatform>>translateTo: aPosition
    "Translate the platform to a particular position"
    self createElement.
    element translateTo: aPosition

```

在执行翻译之前，`translateTo:`方法确保创建了可视元素。处理平台效果的原语是碰撞检测。我们定义了`collide:`方法，该方法测试平台是否与节点冲突:

```
CPlatform>>collide: node
    "Answer whether the platform collides with the node argument"
    ^ node element encompassingRectangle intersects: self element encompassingRectangle

```

注意，`encompassingRectangle`调用指的是 Roassal 提供的方法。包含矩形是 Pharo 提供的`Rectangle`类的一个实例，它包含了可视元素。`collide:`方法返回`true`或`false`，表示提供的`node`是否在平台之上。如果两个包围矩形重叠，则识别出碰撞。如果发生碰撞，必要时调整节点的位置很重要:

```
CPlatform>>adjustNodeIfNecessary: node
    "Answer whether the platform collides with the node"
    | bottomNode topPlatform |
    bottomNode := node element encompassingRectangle bottomCenter y.
    topPlatform := self element encompassingRectangle topCenter y.

    topPlatform < bottomNode
        ifTrue: [ node translateBy: 0 @ (topPlatform - bottomNode) ]

```

这种调整是必要的，因为节点平移是离散的，而不是连续的。因此，一个下落的节点可能在一个平台内，因此应该被转换到平台之上。

## 12.3 定义肌肉

肌肉是这个模拟的核心，是一个复杂的数据结构。肌肉是有力量的振荡刃。它连接两个连接点(即节点)。我们将首先定义`CConnection`类来表示两个节点之间的连接。我们将`CConnection`类定义如下:

```
CVisualElement subclass: #CConnection
    instanceVariableNames: 'node1 node2'
    classVariableNames: ''
    package: 'Creature'

```

我们将`node1`和`node2`定义为由`CNode`类的实例表示的两个极端。第一个端点使用以下公式获得:

```
CConnection>>node1
    ^ node1

```

使用以下方法设置第一个端点:

```
CConnection>>node1: aNode
    node1 := aNode

```

第二个极端是用这种方法得到的:

```
CConnection>>node2
    ^ node2

```

使用以下内容进行设置:

```
CConnection>>node2: aNode
    node2 := aNode

```

肌肉有一个内部计时器来驱动振动。我们将`CMuscle`类定义如下:

```
CConnection subclass: #CMuscle
    instanceVariableNames: 'time time1 time2 length1 length2 strength color'
    classVariableNames: ''
    package: 'Creature'

```

`CMuscle`类有以下变量:

*   由变量`time`表示的内部时钟。

*   `time1`和`time2`是内部时钟用来确定肌肉长度的两个阈值。

*   肌肉的长度在两个值之间摆动— `length1`和`length2`。

*   `strength`代表肌肉受到外力时的阻力有多大，比如相连连接点的重量。

*   `color`变量表示肌肉的颜色。

肌肉长度沿着其内部时钟振荡。模拟振荡的方法很简单，就是让`beat`方法将变量`time`增加`1`:

```
CMuscle>>beat
    "Beating a muscle increases its timer"
    time := time + 1.
    time = self maxTime ifTrue: [ time := 0 ].

```

如果时间达到最大值，则重置为`0`。因此，内部定时器是循环的。当肌肉被创建时，它有一个设置为`0`的定时器。我们如下初始化肌肉:

```
CMuscle>>initialize
    super initialize.
    time := 0.
    color := Color red.

```

肌肉的视觉表现由`createElement`方法给出。肌肉是连接`node1`和`node2`的直线。`createElement`方法定义如下:

```
CMuscle>>createElement
    "A muscle is a transparent line between the two nodes"
    element := RTLine new color: (color alpha: 0.3); width: 5;
    edgeFrom: node1 element to: node2 element

```

肌肉的颜色由以下因素决定:

```
CMuscle>>color: aColor
    "Set the color of the muscle"
    color := aColor

```

请注意，`createElement`方法使颜色半透明。这很有用，因为肌肉确实会重叠。半透明的肌肉让由许多肌肉组成的生物看起来很舒服。

肌肉的长度是可变的。一块肌肉的实际长度不是`length1`就是`length2`。如果肌肉计时器低于下限阈值(即，具有值`self minTime`，则肌肉长度为`length1`；否则，就是`length2`。我们将`length`方法定义如下:

```
CMuscle>>length
    "Maybe rename it to ideal length"
    ^ time < self minTime
       ifTrue: [ length1 ]
       ifFalse: [ length2 ]

```

如果`time`变量的值低于下限阈值，那么我们说我们处于一个周期的开始。如果`time`大于`self minTime`，我们称之为肌肉周期的结束。我们为`length1`和`length2`变量定义了一些访问方法:

```
CMuscle>>length1
    "Length of a muscle at the beginning of a cycle"
    ^ length1

```

使用以下内容设置该值:

```
CMuscle>>length1: aLengthAsInteger
    "Set the muscle length at the beginning of a cycle"
    length1 := aLengthAsInteger

```

类似地，`length2`通过以下方式访问:

```
CMuscle>>length2
    "Length of a muscle at the end of a cycle"
    ^ length2

```

使用以下公式设置第二个长度:

```
CMuscle>>length2: aLengthAsInteger
    "Set the muscle length at the end of a cycle"
    length2 := aLengthAsInteger

```

周期长度由`maxTime`的值给出，该值定义为`time1`和`time2`之间的最大值:

```
CMuscle>>maxTime
    "Return the cycle length"
    ^ time1 max: time2

```

类似地，阈值由`minTime`方法给出:

```
CMuscle>>minTime
    "Return the timer threshold between to switch between length1 and length2"
    ^ time1 min: time2

```

肌肉具有力量，可通过以下方式获得:

```
CMuscle>>strength
    "Return the strength of the muscle"
    ^ strength

```

肌肉的强度用于计算肌肉将施加到四肢节点的力。使用以下内容设置强度:

```
CMuscle>>strength: strengthAsFloat
    "Set the strength that is applied to the extremities"
    strength := strengthAsFloat

```

根据定义，肌肉的内部计时器在每次跳动时都会增加。第一定时器阈值通过下式设置:

```
CMuscle>>time1: anInteger
    time1 := anInteger

```

`time1`值通过以下方式获得:

```
CMuscle>>time1
    ^ time1

```

正如我们将在后面看到的，肌肉属性必须被序列化，以便在遗传算法中从个体进行编码和解码。因此，我们需要访问这些值。第二个时间阈值使用以下公式设置:

```
CMuscle>>time2: anInteger
    time2 := anInteger

```

可以通过以下方式访问它:

```
CMuscle>>time2
    ^ time2

```

每个生物都是随机生成的。生成一个生物并不简单，因为一个形态良好的生物必须将所有节点连接到一些肌肉上，并且两个节点不能有一个以上的肌肉。因此，我们必须监控肌肉在生成过程中如何使用节点。一个简单的方法将有助于测试肌肉是否连接两个指定的节点:

```
CMuscle>>usesNodes: twoNodes
    "The method accepts an array of two nodes as an argument.
    Return true if the muscle connects the two nodes."
    ^ (node1 == twoNodes first and: [ node2 == twoNodes second ]) or:
        [ node1 == twoNodes second and: [ node2 == twoNodes first ] ]

```

肌肉的定义现在完成了。我们的生物会随机生成，也就是说肌肉也会随机生成。下一节定义了肌肉生成器，作为封装生成肌肉的复杂性的一种方式。

## 12.4 生成肌肉

当肌肉被随机生成时，定义肌肉的属性(即`time1`、`time2`、`length1`、`length2`和`strength`)也必须被随机生成。`CMuscleGenerator`类负责生成随机肌肉。肌肉生成器用每个属性的值范围来参数化，用最小值和增量值来表示。我们将`CMuscleGenerator`类定义如下:

```
Object subclass: #CMuscleGenerator
    instanceVariableNames: 'random minStrength deltaStrength minLength deltaLength minTime deltaTime'
    classVariableNames: ''
    package: 'Creature'

```

生成器的初始化是通过分配一些值来完成的，这些值在我们稍后将看到的大多数示例中都很方便:

```
CMuscleGenerator>>initialize
    super initialize.
    self resetSeed.
    minLength := 10.
    deltaLength := 30.
    minTime := 4.
    deltaTime := 200.
    minStrength := 1.
    deltaStrength := 3

```

使用`resetSeed`创建随机数发生器:

```
CMuscleGenerator>>resetSeed
    random := Random seed: 42.

```

值的增量对应于随机选取值的区间。使用以下公式设置增量长度:

```
CMuscleGenerator>>deltaLength: anInteger
    deltaLength := anInteger

```

使用以下公式设置增量强度:

```
CMuscleGenerator>>deltaStrength: anInteger
    deltaStrength := anInteger

```

使用以下公式设置增量时间:

```
CMuscleGenerator>>deltaTime: anInteger
    deltaTime := anInteger

```

长度的最小值可通过以下方式设置:

```
CMuscleGenerator>>minLength: anInteger
    "Set the minimum value a muscle length may have"
    minLength := anInteger

```

类似地，最小强度使用以下公式设置:

```
CMuscleGenerator>>minStrength: anInteger
    "Set the minimum value a muscle strength can have"
    minStrength := anInteger

```

最小时间阈值通过以下方式设置:

```
CMuscleGenerator>>minTime: anInteger
    "Set the minimum value a muscle time threshold can be"
    minTime := anInteger

```

使用专用方法生成长度:

```
CMuscleGenerator>>generateLength
    "Return a length within the specified range"
    ^ minLength + (random nextInt: deltaLength)

```

类似地，强度由以下内容产生:

```
CMuscleGenerator>>generateStrength
    "Return a strength within the specified range"
    ^ random next * deltaStrength + minStrength

```

时间阈值由以下内容生成:

```
CMuscleGenerator>>generateTime
    "Return a time within the specified range"
    ^ (random nextInt: deltaTime) + minTime

```

生成器的一个核心方法是`createMuscleFrom:to:`。该方法用于在两个节点之间产生肌肉，如下所示:

```
CMuscleGenerator>>createMuscleFrom: aNode to: anotherNode
    "Return a new muscle connecting two nodes"
    | m |
    m := CMuscle new.
    m node1: aNode.
    m node2: anotherNode.
    m length1: self generateLength.
    m length2: self generateLength.
    m time1: self generateTime.
    m time2: self generateTime.
    m strength: self generateStrength.
    ^ m

```

应用遗传算法搜索最佳肌肉配置的一个重要方面是充分管理一组值和肌肉定义之间的映射。一块肌肉可以*序列化*成一组值，一组值可以*物化*成一块肌肉。在我们的遗传算法中，这些操作对于从一个给定的个体产生一个生物是必要的。

```
CMuscleGenerator>>serializeMuscle: aMuscle
    "Return an array describing the muscle provided as an argument"
    ^ Array
    with: aMuscle length1
    with: aMuscle length2
    with: aMuscle strength
    with: aMuscle time1
    with: aMuscle time2

```

物化用一组值配置肌肉:

```
CMuscleGenerator>>materialize: values inMuscle: aMuscle
    "Configure the provided muscle with some values"
    aMuscle length1: values first.
    aMuscle length2: values second.
    aMuscle strength: values third.
    aMuscle time1: values fourth.
    aMuscle time2: values fifth

```

在我们的遗传算法中，一个个体包含了一个生物所有肌肉的属性。遗传算法需要产生一个基因，因此，我们需要一种方法来为个体遗传信息中的给定基因位置产生特定值。以下方法满足了这一要求:

```
CMuscleGenerator>>valueForIndex: anIndex
    "Produce a value for a given index of an individual chromosome.
    This method is used to generate a gene in the genetic algorithm"
    | i |
    i := (anIndex - 1) % 5\. "% refers to modulo"
    i = 0 ifTrue: [ ^ self generateLength ].
    i = 1 ifTrue: [ ^ self generateLength ].
    i = 2 ifTrue: [ ^ self generateStrength ].
    i = 3 ifTrue: [ ^ self generateTime ].
    i = 4 ifTrue: [ ^ self generateTime ].
    self error: 'Should not be here'

```

值得注意的是，这三种方法——`serializeMuscle:`、`materialize:inMuscle:`和`valueForIndex:`——严重依赖于属性的顺序。如果要添加与肌肉重量相关的新属性，延迟作用于肌肉)，那么这三种方法必须进行相应的修改。我们的肌肉发生器现在完成了。我们现在能够模拟一个动物形态的生物。

## 12.5 定义生物

我们将一个生物定义为`CCreature`类的实例，如下所示:

```
Object subclass: #CCreature
    instanceVariableNames: 'nodes muscles random muscleGenerator color'
    classVariableNames: ''
    package: 'Creature'

```

生物本质上由一组节点和一组肌肉组成，分别保存在`nodes`和`muscles`变量中。我们将运行我们的遗传算法来优化肌肉的配置。我们肌肉的初始配置是随机的，这就是为什么我们需要一个随机数生成器。肌肉是一种复杂的结构。因此，一个生物需要一个特定的专用物体来创造肌肉，保存在`muscleGenerator`变量中。每个生物也有一个`color`，当屏幕上出现不止一个生物时，这有助于区分它们。

生物初始化如下:

```
CCreature>>initialize
    super initialize.
    nodes := OrderedCollection new.
    muscles := OrderedCollection new.
    random := Random seed: 42.
    muscleGenerator := CMuscleGenerator new.
    color := Color red.

```

肌肉默认为红色。它的颜色可以用这种方法改变:

```
CCreature>>color: aColor
    "Set the color of the creature"
    color := aColor

```

可以使用以下方法生成肌肉并添加到生物身上:

```
CCreature>>addMuscleFrom: aNode to: anotherNode
    "Generate and add a muscle between two nodes"
    muscles add: (muscleGenerator createMuscleFrom: aNode to: anotherNode)

```

为了运行遗传算法，需要知道一个生物有多少肌肉。肌肉数量通过以下方式获得:

```
CCreature>>numberOfMuscles
    "Return the number of muscles defining the creature"
    ^ muscles size

```

生物的每一次跳动都会触发每个节点和每块肌肉的一次跳动。之后，物理规则必须应用于肌肉和节点之间。`beat`方法定义如下:

```
CCreature>>beat
    "Execute a unit of behavior"
    nodes do: #beat.
    muscles do: #beat.
    self reachStable

```

`reachStable`方法，我们将在后面描述，在给定的单位时间内，通过使用物理规则作用于生物。使用`checkForCollision:`方法确定生物和平台之间是否发生碰撞，定义如下:

```
CCreature>>checkForCollision: platforms
    "Check if a creature is on a platform.
    If this is the case, then the variable isOnPlatform of each node
    is set to true"
    nodes do: [ :n | n checkForCollision: platforms ].
    self simulateNoise.

```

我们正在实现的物理引擎是最小的，还远远没有完成。我们需要在物理模拟的方式中添加一些噪声。例如，随机噪声是真实物理世界的一个重要属性，也必须建模。这种噪声对于避免奇异情况是必要的，例如，当所有节点都在相同的 X 或 Y 坐标上时。我们只是通过随机移动一个节点来添加一些噪声:

```
CCreature>>simulateNoise
    "Produce noise in our simulation"
    | direction |
    direction := ((random nextInt: 3) - 2) @ ((random nextInt: 3) - 2).
    (nodes atRandom: random) translateBy: direction

```

建立这个生物模型的所有必要代码都已经就绪。下一节重点是创建生物。

## 12.6 创造生物

即使我们会制造出形状简单的生物，手动创造生物也是繁琐的。我们可以定义一些专用的方法。给生物添加节点是通过`configureNodes:`方法实现的，定义如下:

```
CCreature>>configureNodes: nbNodes
    "Add a number of nodes in our creature"
    nbNodes timesRepeat: [ nodes add: CNode new createElement ]

```

使用以下方法创建一个类似球的形状:

```
CCreature>>configureBall: numberOfNodes
    "Produce a ball-like creature"
    | existingMuscles |
    muscleGenerator := CMuscleGenerator new
        minStrength: 0.01;
        deltaStrength: 0.5;
        minLength: 10;
        deltaLength: 80;
        deltaTime: 200;
        minTime: 20.

    "Add some nodes"
    self configureNodes: numberOfNodes.

    "Connect each node with all the other nodes"
    existingMuscles := OrderedCollection new.
    nodes do: [ :n1 |
        (nodes copyWithout: n1) do: [ :n2 |
            (existingMuscles includes: n1 -> n2) ifFalse: [
            self addMuscleFrom: n1 to: n2.
            existingMuscles add: n1 -> n2; add: n2 -> n1 ] ] ].

"Create the visual elements"
self createElements.
self randomlyLocateNodes

```

`configureBall:`将组成球的节点数作为参数。所有节点都连接到所有其他节点。因此，一个球生物将包含许多肌肉，这意味着肌肉应该具有较低的强度以拥有稳定的系统。我们使用`existingMuscles`变量来跟踪我们在算法中创建的肌肉。这对于防止在两个节点之间添加两块肌肉是必要的。

使用`createElements`方法创建图形元素:

```
CCreature>>createElements
    "Force the creation of all graphical elements for nodes and muscles"
    nodes do: #createElement.
    muscles do: [ :m | m color: color ].
    muscles do: #createElement.

```

节点和肌肉服从牛顿物理定律，使用以下方法定义:

```
CCreature>>reachStable
    "Apply the physical law on a creature"
    | n1 n2 delta actualLength unit force |
    nodes do: #resetForce.
    muscles do: [ :m |
        n1 := m node1.
        n2 := m node2.
        delta := n2 position - n1 position.
        actualLength := delta r max: 1.
        unit := delta / actualLength.
        force := 0.1 * m strength * (actualLength - m length) * unit.
        n1 addForce: force.
        n2 addForce: force negated ].

```

节点上的外力首先被抵消。然后我们根据肌肉的力量来计算这个力。请注意，此力应用于一个节点，相反的力应用于第二个末端节点。

我们的物理引擎的核心和这个生物的模型现在都已经定义好了。我们需要将所有组件连接在一起，并准备好模型以供遗传算法处理。

### 12.6.1 生物的序列化和物化

当我们挂钩我们的遗传算法时，将个体遗传信息转换成一组数字是至关重要的。这些数字将代表生物肌肉的属性。使用以下方法对生物进行序列化:

```
CCreature>>serialize
    "Serialize the creature into an array of numbers"
    ^ (muscles
        flatCollect: [ :m |
            muscleGenerator serializeMuscle: m ]) asArray

```

相反的操作，从一组数值中具体化一个生物，是通过这种方法实现的:

```
CCreature>>materialize: anArrayOfValues
    "Materialize a array of numbers into a creature"
    | valuesPerMuscles |
    valuesPerMuscles :=
        anArrayOfValues groupsOf: 5 atATimeCollect: [ :v | v ].
    muscles with: valuesPerMuscles do: [ :m :values |
        muscleGenerator materialize: values inMuscle: m ]

```

正如我们所见，每块肌肉都有五个属性。因此，`materialize:`方法将`anArrayOfValues`中给定的值分组到大小为`5`的数组中。

### 12.6.2 访问器和实用程序方法

这种生物定义的最大部分已经给出了。生物的肌肉通过以下方式获得:

```
CCreature>>muscles
    "The muscles composing the creature"
    ^ muscles

```

组成一个生物的节点可以通过以下方式访问:

```
CCreature>>nodes
    "The nodes composing the creature"
    ^ nodes

```

生物的位置被计算为组成该生物的节点的平均位置。

当我们应用遗传算法时，知道生物的位置是必要的，因为适应度将基于生物走过的距离。`position`方法定义如下:

```
Creature>>position
    "Return the position of the creature, as the average position of the nodes"
    ^ (self nodes collect: #position) sum / self nodes size

```

在模拟开始时，生物必须位于主平台上方的位置`0@0`:

```
CCreature>>resetPosition
    "Locate the creature at the initial position"
    self translateTo: 0 @ 0

```

在应用物理规则之前，重要的是节点不都在相同的位置。我们使用`randomlyLocateNodes`为每个节点随机分配一个位置:

```
CCreature>>randomlyLocateNodes
    "Assign each node to a random position"
    nodes
        do: [ :n | n translateBy: (random nextInt: 50) @ (random
            nextInt: 50) ]

```

使用以下方法可以将生物移动到给定的位置:

```
CCreature>>translateTo: aPoint
    "Translate a creature to a specified position"
    | averageCenter delta |
    averageCenter := self position.
    delta := aPoint - averageCenter.
    self nodes do: [ :n | n translateBy: delta ]

```

本节总结了该生物的定义。生物必须生活在一个世界中，所以我们在下一节定义一个世界。

## 12.7 定义世界

世界被定义为一组生物、一组平台和一个全局计时器。在我们的例子中，一个世界被定义为一个种族的宿主。我们将`CWorld`类定义如下:

```
CVisualElement subclass: #CWorld
    instanceVariableNames: 'creatures time platforms'
    classVariableNames: ''
    package: 'Creature'

```

创建时，世界被初始化为空。`initialize`方法定义如下:

```
CWorld>>initialize
    super initialize.
    creatures := OrderedCollection new.
    platforms := OrderedCollection new.
    time := 0.
    self createElement.
    self addGround

```

世界与漫游视图相关联，定义如下:

```
CWorld>>createElement
    "The visual representation of a world is a Roassal view"
    element := RTView new.

```

地面被表示为一个大平台:

```
World>>addGround
    "Define the ground of the world"
    | platform |
    platform := CPlatform new width: self groundLength + 500.
    "We give an extra distance of 500 to make sure there is no issue with the border"
    self addPlatform: platform.
    "The platform is located below where creatures will be initially located"
    platform translateTo: self groundLength / 2 @ 100

```

地面的长度由`groundLength`方法设定:

```
CWorld>>groundLength
    "Set the length of the ground platform"
    ^ 5000

```

使用以下方法可以将生物添加到世界中:

```
CWorld>>addCreature: aCreature
    "Add a creature to the world"
    creatures add: aCreature.

    "Add all the graphical elements of the creature in the view"
    element addAll: (aCreature nodes collect: #element).
    element addAll: (aCreature muscles collect: #element).

    "Move the creature at the initial position"
    aCreature resetPosition.

```

当一个生物被添加到世界中时，所有源自肌肉和节点的图形元素都会被添加到视图中。类似地，使用以下代码将平台添加到世界中:

```
CWorld>>addPlatform: aPlatform
    "Add a platform to the world"
    platforms add: aPlatform.
    aPlatform createElement.
    element add: aPlatform element.

```

一个世界有一个全球计时器。每次刷新窗口时，计时器都会增加，由操作系统决定。`beat`方法定义如下:

```
CWorld>>beat
    "Trigger a global beat"
    time := time + 1.
    creatures do: [ :c | c beat; checkForCollision: platforms ]

```

在每一拍，物理规则必须适用于每一个生物。请注意，生物之间不能相互作用。

为了让世界更有吸引力，装饰很重要。我们用下面的方法给一个世界添加一些塔:

```
CWorld>>addPylons
    "Add pylons to the world as decorating elements"
    (0 to: self groundLength by: 100)
        do: [ :flagPosition |
            | pylon |
            pylon := RTBox new
                color: Color green darker;
                width: 3;
                height: 100;
                elementOn: flagPosition.
            element add: pylon.
            pylon @ RTLabeled.
            pylon translateTo: flagPosition @ 50.
            pylon pushBack ]

```

`open`方法创建世界的可视化表示，添加一些装饰(计时器和挂架的标签)，触发动画，并打开窗口:

```
CWorld>>open
    "Build the visual representation of the world"
    | lbl animation |
    creatures do: #resetPosition.
    lbl := (RTLabel new elementOn: time) setAsFixed; yourself.
    element add: lbl.
    lbl translateBy: 80 @ 30.
    animation := RTActiveAnimation new
        intervalInMilliseconds: 10;
        blockToExecute: [ | p |
            self beat.
            lbl trachelShape text: time asString.
            p := creatures first position x @ 0.
            element canvas camera translateTo: p.
            element signalUpdate.
            p x > self groundLength
                ifTrue: [ element removeAnimation: animation ] ].
    element addAnimation: animation.
    self addPylons.
    element canvas camera scale: 2.2.
    ^ element open

```

这个世界可能不止有一种生物。第一个生物的进程在模拟过程中被监控。如果第一个生物到达地面平台的末端，动画结束。

## 12.8 冷运转

我们现在已经定义了让一个生物“活”起来的所有相关要素我们现在可以打开一个世界并添加一个生物到其中(见图 [12-2](#Fig2) ):

![../images/484203_1_En_12_Chapter/484203_1_En_12_Fig2_HTML.jpg](../images/484203_1_En_12_Chapter/484203_1_En_12_Fig2_HTML.jpg)

图 12-2

未经训练的动物

```
creature := CCreature new configureBall: 10.
c := CWorld new.
c addCreature: creature.
c open

```

图 [12-3](#Fig3) 显示了脚本的结果。几个生物可以被添加到一个世界中，每个都有特定的位置、节点数和颜色:

![../images/484203_1_En_12_Chapter/484203_1_En_12_Fig3_HTML.jpg](../images/484203_1_En_12_Chapter/484203_1_En_12_Fig3_HTML.jpg)

图 12-3

几种未经训练的动物

```
redCreature := CCreature new configureBall: 7.
blueCreature := CCreature new color: Color blue; configureBall: 10.
yellowCreature := CCreature new color: Color yellow; configureBall: 15.

c := CWorld new.
c addCreature: redCreature.
c addCreature: blueCreature.
c addCreature: yellowCreature.
c open.
blueCreature translateTo: 100 @ -50.
yellowCreature translateTo: 200 @ -50.

```

在这个阶段，生物没有特定的目标。下一章将讲述如何让生物进化。

## 12.9 我们在本章中看到了什么？

这一章的重点是定义基础设施来建模和建造动物形态的生物。这是一个很长的章节。我们必须提供足够的代码来构建一个有趣的代码库，我们可以在下一章中用它进行实验。

这一章包括:

*   一个简单的物理引擎，支持肌肉和节点。

*   生物的定义和它可以生存的世界。

请注意，我们可以在创建生物模型时添加骨骼。一旦我们有了骨骼的概念，我们就可以建造骨骼。尽管它很吸引人，但它会显著增加源代码的数量，这也是我们没有考虑它的原因。