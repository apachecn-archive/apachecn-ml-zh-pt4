# 6.矩阵库

在前面的章节中，我们介绍了由层和神经元(即`NeuronLayer`和`Neuron`的实例)组成的神经网络的实现。尽管具有指导意义，但该实现并不反映实现神经网络的经典方式。层可以表示为权重矩阵和偏差向量。这是大多数构建神经网络的库(例如 TensorFlow 和 PyTorch)的实际操作方式。

这一章展示了一个构建和操作矩阵的小库。本章是下一章的重要基础，下一章是关于如何用矩阵实现网络。矩阵是一种特殊的数据结构，在 Pharo 中不能有效地实现它的运算。我们将用 C 编写这些代价高昂的操作，但要让它们在 Pharo 中可以访问。

除了定义一个矩阵库，本章还强调了 Pharo 的一个特殊方面，那就是外来函数接口(FFI)的使用。每当希望让 Pharo 使用用 C 或 C++编程语言编写的外部库时，这都是一个相关的机制。例如，TensorFlow 是用 C++编写的，可以使用本章介绍的相同技术从 Pharo 访问它。

这一章很长，包含许多相互依赖的方法。该章在发挥作用之前需要得到充分实施。

## 6.1 语言中的矩阵运算

Pharo 没有提供操作矩阵的内置特性。尽管我们可以在 Pharo 中实现它们，但它的性能会很差。相反，我们将用 C 编写一个小的库来支持基本的 C 操作。用下面的 C 代码创建一个名为`matrix.c`的文件:

```
void dot(double *m1, int m1_nb_rows, int m1_nb_columns, double *m2,
                     int m2_nb_rows, int m2_nb_columns, double *res) {
       int col, row, k;
       for (col = 0; col < m2_nb_columns; col++) {
            for (row = 0; row < m1_nb_rows; row++) {
                 double tmp = 0;
            for (k = 0; k < m2_nb_rows; k++)
                 tmp += m1[row * m1_nb_columns + k] * m2[k *
                      m2_nb_columns + col];
                 res[row * m2_nb_columns + col] = tmp;
} } }
void sub(double *m1, int nb_rows, int nb_columns,
                     double *m2, double *res) {
       int col, row;
       for (col = 0; col < nb_columns; col++) {
              for (row = 0; row < nb_rows; row++) {
                    res[row * nb_columns + col] =
                      m1[row * nb_columns + col] - m2[row * nb_columns +
col];
} } }
void add(double *m1, int nb_rows, int nb_columns,
                     double *m2, double *res) {
       int col, row;
        for (col = 0; col < nb_columns; col++) {
              for (row = 0; row < nb_rows; row++) {
                        res[row * nb_columns + col] =
                       m1[row * nb_columns + col] + m2[row * nb_columns +
col];
} } }

```

这个小库由三个 C 函数组成:

*   `dot`执行矩阵乘法

*   `sub`从一个矩阵减去另一个矩阵

*   `add`对两个矩阵求和

关于这个 C 文件我们就不赘述了。它只是应用一些基本的矩阵运算。每个函数都将指向一些矩阵及其形状的指针作为参数。库必须被编译，这意味着我们不可避免地要接近操作系统。本章将只涉及 MacOS 和 Linux。两个平台都提供 gcc 标准编译器。假设安装了 gcc 工具套件，并且您使用的是 MacOS，您应该在终端中键入以下内容:

```
gcc -dynamiclib -o matrix.dylib matrix.c

```

在 Linux 上，您需要执行以下命令:

```
gcc -c -Wall -Werror -fpic matrix.c
gcc -shared -o libmatrix.so matrix.o

```

我们的矩阵文件被编译成一个动态库，可以在 Pharo 中加载。编译产生一个动态库。在 OSX 上，生成的文件被命名为`matrix.dylib`，在 Linux 上被称为`libmatrix.so`。不管怎样，这个文件*必须位于`.image`文件旁边的*中，在同一个文件夹中。

## 6.2 矩阵类

我们现在可以编写名为`MMatrix`的 Pharo 类，它将使用我们的 C 库。请注意，Pharo 7 包含一个名为`Matrix`的不推荐使用的类，它对于我们的目的来说并不真正有用。这就是为什么我们的类以一个额外的`M`字符为前缀。在名为`Matrix`的新包中，我们定义了这个类:

```
Object subclass: #MMatrix
        instanceVariableNames: 'nbRows nbColumns array'
        classVariableNames: ''
        package: 'Matrix'

```

前两个变量描述矩阵的形状，而`array`变量以线性方式引用包含矩阵实际值的数组。这个数组必须可以从 Pharo 和我们的 C 库中访问。

在类`MMatrix`的类端，我们定义了许多创建矩阵的有用方法。您需要将类浏览器切换到类模式来定义类方法。`newFromArrays:`方法从数组集合中创建一个矩阵:

```
MMatrix class>>newFromArrays: arrays
        "Create a matrix from an array containing the structured
        values of the matrix. Example of matrix creations:
        MMatrix newFromArrays: #(#(1 2 3) #(4 5 6))
        MMatrix newFromArrays: #(#(1 2 3))
        MMatrix newFromArrays: #(#(1) #(2) #(3))
        "
        ^ self basicNew
              initializeRows: arrays size columns: arrays first size;
              fromContents: (arrays flatCollect: #yourself);
              yourself

```

我们还需要一个较低的级别来创建一个矩阵，只需提供矩阵的形状。这假定矩阵内容是稍后设置的。考虑这个新的类方法:

```
MMatrix class>>newRows: numRows columns: numColumns
        "Create a matrix with a given shape"
        ^ self basicNew
        initializeRows: numRows columns: numColumns;
        yourself

```

然后我们定义一个初始化矩阵的方法:

```
MMatrix>>initializeRows: numRows columns: numColumns
        self initialize.
        nbRows := numRows.
        nbColumns := numColumns.
        array := self newArray

```

该数组用于保存矩阵内容，并使用`newArray`进行定义:

```
MMatrix>>newArray
        "Create an array used to contains the store the matrix content"
        ^ FFIExternalArray
              newType: 'double'
              size: nbColumns * nbRows

```

`FFIExternalArray`类表示一个数组，其元素是某种外部类型的值。在我们的例子中，我们将把矩阵值编码为一个`double`，它是一个 64 位编码的浮点值。必须从其他对象访问该数组:

```
MMatrix>>array
        "The array containing matrix' values"
        ^ array

```

存在于 Pharo 内存空间中的外来对象需要从我们的外部库访问。一个有用的类表示 C 库使用的内存地址。`FFIExternalArray`类提供了`getHandle`方法来访问内存位置:

```
MMatrix>>getHandle
        "Return the handle of the foreign object.
        This allows the array to be accessed from C"
        ^ array getHandle

```

简便的`asArray`方法在测试中很有用。我们将在验证矩阵是否正确创建时使用它:

```
MMatrix>>asArray
         "Return a linear array of the matrix values"
         ^ array asArray

```

在某些情况下，创建矩阵时必须提供一个句柄。下面的方法解决了这个问题:

```
MMatrix class>>newHandle: aHandle rows: numRows columns: numColumns
        "Create a matrix with a provided content. Useful when creating
             a matrix after an FFI operation"
        ^ self basicNew
             initializeHandle: aHandle rows: numRows columns: numColumns;
             yourself

```

方法用一个句柄和一个特定的形状初始化一个矩阵:

```
MMatrix>>initializeHandle: aHandle rows: numRows columns: numColumns
        "Initialize the matrix"
        self initialize.
        nbRows := numRows.
        nbColumns := numColumns.
        array := self newArrayFromHandle: aHandle

```

以下工厂方法使用给定句柄创建外部数组:

```
MMatrix>>newArrayFromHandle: aHandle
         "Create an external array using a handle"
         ^ FFIExternalArray
                fromHandle: aHandle
                type: 'double'
                size: nbColumns * nbRows

```

我们需要一些实用的方法来访问矩阵的形状:

```
MMatrix>>nbRows
        "Number of rows defined in the matrix"
        ^ nbRows

```

和

```
MMatrix>>nbColumns
        "Number of columns defined in the matrix"
        ^ nbColumns

```

使用`size`访问矩阵值的数量，如下所示:

```
MMatrix>>size
         "The number of values contained in the matrix"
         ^ nbColumns * nbRows

```

矩阵可以用一组线性值来填充:

```
MMatrix>>fromContents: content
         "Initialize the matrix with a linear content"
         self assert: [ content size = (nbColumns * nbRows) ] description:
             'size mismatch'.
         content doWithIndex: [ :v :i | array at: i put: v ]

```

这些方法将在下面的小节中进行适当的测试。

## 6.3 创建单元测试

我们现在可以编写单元测试了。`MMatrixTest`类将包含我们所有关于`MMatrix`的测试。考虑下面的类:

```
TestCase subclass: #MMatrixTest
       instanceVariableNames: ''
       classVariableNames: ''
       package: 'Matrix'

```

作为第一个测试，我们可以改变在`MMatrix`上定义的`creation`方法的正确行为:

```
MMatrixTest>>testCreation
         | m |
         m := MMatrix newFromArrays: #(#(1 2) #(3 4)).
         self assert: m asArray equals: #(1.0 2.0 3.0 4.0)

```

在本章的剩余部分，我们将扩展`MMatrixTest`类。

## 6.4 访问和修改矩阵的内容

能够方便地更新矩阵内容是我们应该考虑的第一步。可使用`at:`消息访问矩阵的内容。此方法将点作为参数，如下所示:

```
MMatrix>>at: aPoint
        "Access an element of the matrix"
        ^ array at: ((aPoint x - 1) * nbColumns + (aPoint y - 1)) + 1

```

我们可以如下测试`at:`方法:

```
MMatrixTest>>testAt
        | m |
        m := MMatrix newFromArrays: #(#(1 2) #(3 4)).
        self assert: (m at: 1 @ 1) equals: 1.
        self assert: (m at: 1 @ 2) equals: 2.
        self assert: (m at: 2 @ 1) equals: 3.
        self assert: (m at: 2 @ 2) equals: 4.

```

类似地，我们需要提供一种方法来修改矩阵的内容。`at:put:`方法在给定位置插入一个值:

```
MMatrix>>at: aPoint put: aNumber
         "Modify an element of the matrix"
         array at: ((aPoint x - 1) * nbColumns + (aPoint y - 1)) + 1 put:
             aNumber asFloat

```

为了简化测试，我们添加了一个方便的转换方法:

```
MMatrix>>asStructuredArray
         "Return a structured array that describe the matrix"
         ^ (1 to: nbRows) collect: [ :i | self atRow: i ] as: Array

```

`atRow:`方法返回给定索引的水平值。

```
MMatrix>>atRow: rowNumber
         "Return a particular row"
          (rowNumber between: 1 and: rowNumber)
                 ifFalse: [ self error: 'index out of range' ].
         ^ (1 to: nbColumns) collect: [ :x | self at: rowNumber @ x ]

```

一个简单的测试说明了`at:put:`的用法:

```
MMatrixTest>>testAtPut
        | m |
        m := MMatrix newFromArrays: #(#(1 2) #(3 4)).
        m at: 2 @ 1 put: 10.0.
        self assert: (m at: 2 @ 1) equals: 10.0.
        self assert: m asStructuredArray equals: #(#(1 2) #(10 4))

```

注意，我们使用坐标`row @ column`来引用元素。这种访问矩阵元素的方式接近于线性代数中传统使用的数学符号。

当我们在一个网络中进行预测时，我们将需要获得一个矩阵的最大值。我们可以简单地定义如下:

```
MMatrix>>max
        "Return the maximum value of the matrix"
        ^ self asArray max

```

相应的测试如下:

```
MMatrixTest>>testMax
        | m |
        m := MMatrix newFromArrays: #(#(1 2) #(3 4)).
        self assert: m max equals: 4.

```

我们已经布置了必要的基础设施来定义一些操作。下面几节涵盖了我们将在神经网络中使用的操作。

## 6.5 求和矩阵

两个矩阵可以求和。该操作假设两个矩阵具有完全相同的维数。我们可以用`+`的方法定义总和。此方法接受另一个与接收方大小相同的矩阵，即垂直向量(即只有一列的矩阵):

```
MMatrix>>+ matrixOrVector
         "Add either a matrix or a vector to the receiver.
         The argument could either be a matrix of the same size or
         a vector. A new matrix is returned as result"
         | m |
          ((nbRows = matrixOrVector nbRows) and: [ nbColumns = matrixOrVector
                nbColumns ])
               ifTrue: [ ^ self add: matrixOrVector ].
         matrixOrVector nbColumns ~= 1 ifTrue: [ self error: 'not a n * 1
               vector' ].
         m := matrixOrVector stretchToColumns: nbColumns.
         ^ self + m

```

由于操作的复杂性，添加包括几个步骤。我们定义`add:`方法:

```
MMatrix>>add: aMatrix
        "Add two matrices, the receiver and the argument, and produces
              a new matrix"
        | result resultArray |
        nbColumns = aMatrix nbColumns ifFalse: [self error: 'dimensions
              do not conform'].
        nbRows = aMatrix nbRows ifFalse: [self error: 'dimensions do not
              conform'].

        resultArray := ByteArray new: (nbRows * aMatrix nbColumns * 8).
        self assert: [ nbRows * nbColumns = array size ].
        self assert: [ aMatrix nbRows * aMatrix nbColumns = aMatrix size ].
        self assert: [ nbRows * aMatrix nbColumns * 8 = resultArray size ].
        self
               add: self getHandle with: nbRows with: nbColumns with: aMatrix
                    getHandle
               in: resultArray.
        result := MMatrix newHandle: resultArray rows: nbRows columns:
               nbColumns.
        ^ result

```

`add:`方法创建一个新的矩阵，并从我们的 C 库中调用`add`函数。在 MacOS 上，您需要如下定义方法:

```
MMatrix>>add: m1 with: nb_rows with: nb_columns with: m2 in: res
        ^ self
              ffiCall: #(void add(double *m1, int nb_rows, int nb_columns,
                        double *m2,
                        double *res))
              module: 'matrix.dylib'

```

注意，在 Linux 上，需要用`'libmatrix.so'`替换`'matrix.dylib'`。我们可以通过对两个矩阵求和来测试这一点:

```
MMatrixTest>>testAddition1
        | m1 m2 |
        m1 := MMatrix newFromArrays: #(#(1 2 3) #(4 5 6)).
        m2 := MMatrix newFromArrays: #(#(4 5 6) #(1 2 3)).
        self assert: (m1 + m2) asStructuredArray equals: #(#(5.0 7.0 9.0)
             #(5.0 7.0 9.0))

```

我们还可以尝试向自身添加一个矩阵:

```
MMatrixTest>>testAddition2
         | m |
         m := MMatrix newFromArrays: #(#(1 2 3) #(4 5 6)).
         self assert: (m + m) asStructuredArray equals: #(#(2.0 4.0 6.0)
              #(8.0 10.0 12.0))

```

矩阵的元素可以水平求和。正如我们将在下一章看到的，当我们实现反向传播算法时，这个操作是很重要的。考虑以下`sumHorizontal`方法:

```
MMatrix>>sumHorizontal
        "Horizontal summing"
        | result sum |
        result := MMatrix newRows: nbRows columns: 1.
        1 to: nbRows do: [ :y |
              sum := 0.
              1 to: nbColumns do: [ :x |
                   sum := sum + (self at: y @ x) ].
              result at: y @ 1 put: sum ].
        ^ result

```

以下试验方法中提供了一个`sumHorizontal`的例子:

```
MMatrixTest>>testSumHorizontal
        | m expectedResult |
        m := MMatrix newFromArrays: #(#(1.0 2.0) #(3.0 4.0) #(5.0 6.0)).
        expectedResult := MMatrix newFromArrays: #(#(3.0) #(7.0) #(11.0)).
        self assert: m sumHorizontal asStructuredArray equals:
            expectedResult asStructuredArray

```

## 6.6 打印矩阵

能够打印矩阵对于了解矩阵是如何制作的至关重要。`printOn:`方法返回接收相应消息的对象的文本表示。因此，我们将在`MMatrix`类中重新定义它:

```
MMatrix>>printOn: aStream
         "Print the matrix in the stream, with 4 decimal for each value"
         self printOn: aStream round: 4

```

我们将处理 64 位浮点值的矩阵。为了使打印有效，我们需要限制小数位数:

```
MMatrix>>printOn: aStream round: nbDecimals
        "Print the receiver matrix into a stream. All numerical value are
             truncated to a fixed number of decimals"
        aStream nextPutAll: '('.
         (1 to: nbRows)
              do: [ :r |
                        (self atRow: r)
                             do: [ :each | aStream nextPutAll: (each round:
                                  nbDecimals) printString ]
                             separatedBy: [ aStream space ]]
              separatedBy: [ aStream cr ].
        aStream nextPutAll: ' )'.

```

我们现在可以在操场上测试我们的代码了。考虑下面的代码片段:

```
m := MMatrix newFromArrays: #(#(1 2 3) #(4 5 6)).
m + m

```

打印此代码应产生以下内容:

```
(2.0 4.0 6.0
8.0 10.0 12.0)

```

## 6.7 表达载体

向量是一个只有一列的矩阵。例如，表达式`MMatrixnewFromArrays: #(#(1)#(2)#(3))`创建了一个包含三个元素的向量。我们提供了一个定义向量的实用方法:

```
MMatrix class>>newFromVector: array
         "Create a Nx1 matrix from an array of numbers (N = array size)"
         ^ self basicNew
               initializeRows: array size columns: 1;
               fromContents: array;
               yourself

```

`newFromVector:`方法需要一个平面 Pharo 数组。这里有一个例子:

```
MMatrixTest>>testVectorCreation
        | v |
        v := MMatrix newFromVector: #(1 2 3).
        self assert: v nbColumns equals: 1.
        self assert: v nbRows equals: 3.
        self assert: v asStructuredArray equals: #(#(1) #(2) #(3))

```

反向传播算法必须将一个向量拉伸成一个矩阵。它通过将向量并置几次来将向量转换成矩阵。我们定义以下方法:

```
MMatrix>>stretchToColumns: nbOfColumns
        "Stretch a vertical vector in a column."
        | content result |
        content := OrderedCollection new.
        1 to: nbRows do: [ :row |
             1 to: nbOfColumns do: [ :columns |
                  content add: (self at: row @ 1) ] ].
        result := MMatrix newRows: nbRows columns: nbOfColumns.
        result fromContents: content.
        ^ result

```

打印表达式(`MMatrixnewFromVector: => #(1 2 3 4)) stretchToColumns: 5`

结果如下:

```
(1.0 1.0 1.0 1.0 1.0
2.0 2.0 2.0 2.0 2.0
3.0 3.0 3.0 3.0 3.0
4.0 4.0 4.0 4.0 4.0 )

```

测试可定义如下:

```
MMatrixTest>>testStretching
        | m |
        m := (MMatrix newFromVector: #(1 2 3 4)) stretchToColumns: 5.
        self assert: m nbRows equals: 4.
        self assert: m nbColumns equals: 5.
        self assert: (m atRow: 1) equals: #(1 1 1 1 1).
        self assert: (m atRow: 3) equals: #(3 3 3 3 3).

```

## 6.8 因素

在反向传播算法的几个部分中，能够变换矩阵和乘矩阵是很重要的。我们将首先定义一种变换矩阵的通用方法:

```
MMatrix>>collect: aOneArgBlock
        "Return a new matrix, for which each matrix element is
              transformed using the provided block"
        | result |
        result := MMatrix newRows: nbRows columns: nbColumns.
        1 to: nbRows do: [ :y |
             1 to: nbColumns do: [ :x |
                   result at: y @ x put: (aOneArgBlock value: (self at: y @ x))
              ] ].
        ^ result

```

下面是一个简单的测试，它向每个矩阵元素添加一个值:

```
MMatrixTest>>testCollect
        | m expectedMatrix |
        m := MMatrix newFromArrays: #(#(1 2 3) #(4 5 6)).
        expectedMatrix := MMatrix newFromArrays: #(#(2 3 4) #(5 6 7)).
        self assert: (m collect: [ :v | v + 1]) asStructuredArray equals:
            expectedMatrix asStructuredArray

```

矩阵的元素可以乘以一个数值因子。为此，我们定义了`*`方法:

```
MMatrix>>* aFactor
        "Multiply each element of the matrix by a factor"
        ^ self collect: [ :v | v * aFactor ]

```

我们可以将这种方法应用于一个向量来测试:

```
MMatrixTest>>testMultiplicationOnVector
        | x |
        x := MMatrix newFromVector: #(1 2 3 4).
        self assert: (x * 5) asStructuredArray equals: #(#(5.0) #(10.0)
            #(15.0) #(20.0))

```

类似地，我们可以在矩阵上测试乘法:

```
MMatrixTest>>testMultiplicationOnMatrix
        | x |
        x := MMatrix newFromArrays: #(#(1 2 3 4) #(10 20 30 40)).
        self assert: (x * 5) asStructuredArray
            equals: #(#(5.0 10.0 15.0 20.0) #(50.0 100.0 150.0 200.0))

```

另一个相关操作是将两个矩阵逐元素相乘:

```
MMatrix>>multiplyPerElement: mat
        "Multiply two matrices element-wise"
        | r |
        self assert: [ nbRows = mat nbRows ].
        self assert: [ nbColumns = mat nbColumns ].
        r := MMatrix newRows: nbRows columns: nbColumns.
        r fromContents: (self asArray with: mat array asArray collect: [ :a
             :b | a * b ]).
        ^ r

```

该方法可按如下方式进行测试:

```
MMatrixTest>>testMultiplicationPerElement
        | v1 v2 expectedVector |
        v1 := MMatrix newFromVector: #(1 2 3).
        v2 := MMatrix newFromVector: #(10 20 30).
        expectedVector := MMatrix newFromVector: #(10 40 90).
        self assert: (v1 multiplyPerElement: v2) asArray
                   equals: expectedVector asArray

```

## 6.9 将矩阵除以一个因子

按照与上一节相同的方式，我们可以将矩阵除以一个因子:

```
MMatrix>>/ value
         "Divide each element of the matrix by a value"
         ^ self collect: [ :v | v / value ]

```

该方法可通过以下方式进行测试:

```
MMatrixTest>>testDivision
         | m |
         m := MMatrix newFromArrays: #(#(1 2 3) #(4 5 6)).
         self assert: (m / 2) asStructuredArray equals: #(#(0.5 1.0 1.5)
             #(2.0 2.5 3.0))

```

## 6.10 矩阵产品

我们使用两种方法`+*`和`dot:`定义矩阵乘积。前者是后者的捷径:

```
MMatrix>>+* anotherMatrix
        "Shortcut for the dot operator between matrices"
        ^ self dot: anotherMatrix

```

`dot:`方法定义如下:

```
MMatrix>>dot: anotherMatrix
        "Compute the dot product between the receiving matrix and the
              argument"
        | result resultArray |
        nbColumns = anotherMatrix nbRows ifFalse: [self error:
              'dimensions do not conform'].
        self assert: [ nbRows * nbColumns = array size ].
        self assert: [ anotherMatrix nbRows * anotherMatrix nbColumns =
              anotherMatrix size ].
        resultArray := ByteArray new: (nbRows * anotherMatrix nbColumns * 8).
        self
             dot: self getHandle with: nbRows with: nbColumns
             with: anotherMatrix getHandle
             with: anotherMatrix nbRows with: anotherMatrix nbColumns in:
                  resultArray.
        result := MMatrix
             newHandle: resultArray
             rows: nbRows
             columns: anotherMatrix nbColumns.
        ^ result

```

Pharo 代码和 C 库之间的连接在下面的方法中定义:

```
MMatrix>>dot: array1 with: m1_nb_rows with: m1_nb_columns with: array2
        with: m2_nb_rows with: m2_nb_columns in: res
        "Invoke the C library to perform the dot operator"
        ^ self
              ffiCall: #(void dot(
                   void *array1, int m1_nb_rows, int m1_nb_columns,
                   void *array2, int m2_nb_rows, int m2_nb_columns, void *res))
              module: 'matrix.dylib'

```

在 Linux 上，你需要用`'libmatrix.so'`替换`'matrix.dylib'`。您可以使用以下测试方法测试此代码:

```
MMatrixTest>>testMatrixProduct
        | m1 m2 |
        m1 := MMatrix newFromArrays: #(#(1 2 3 4) #(5 6 7 8)).
        m2 := MMatrix newFromArrays: #(#(1 2) #(3 4) #(5 6) #(7 8)).
        self assert: (m1 +* m2) asStructuredArray equals: #(#(50.0 60.0)
            #(114.0 140.0))

```

## 6.11 矩阵减法

减去矩阵是机器学习中的另一个相关操作。我们定义以下快捷方式:

```
MMatrix>>- anotherMatrix
        "Subtract a matrix from the receiver matrix"
        ^ self sub: anotherMatrix

```

该快捷方式调用`sub:`方法:

```
MMatrix>>sub: anotherMatrix
         | result resultArray |
         nbColumns = anotherMatrix nbColumns ifFalse: [self error: '
               dimensions do not conform'].
         nbRows = anotherMatrix nbRows ifFalse: [self error: 'dimensions
               do not conform'].

         resultArray := ByteArray new: (nbRows * anotherMatrix nbColumns * 8).

         self assert: [ nbRows * nbColumns = array size ].
         self assert: [ anotherMatrix nbRows * anotherMatrix nbColumns =
              anotherMatrix size ].
         self assert: [ nbRows * anotherMatrix nbColumns * 8 = resultArray
              size ].

         self
              sub: self getHandle with: nbRows with: nbColumns with:
                   anotherMatrix getHandle
              in: resultArray.
         result := MMatrix newHandle: resultArray rows: nbRows columns:
              nbColumns.
         ^ result

```

我们的 C 库通过以下方法使用:

```
MMatrix>>sub: m1 with: nb_rows with: nb_columns with: m2 in: res
        ^ self
              ffiCall: #(void sub(double *m1, int nb_rows, int nb_columns,
                       double *m2, double *res))
              module: 'matrix.dylib'

```

注意，在 Linux 上，需要用`'libmatrix.so'`替换`'matrix.dylib'`。一个简单的测试说明了矩阵减法的行为:

```
MMatrixTest>>testSub
        | m1 m2 |
        m1 := MMatrix newFromArrays: #(#(1 2 3 4) #(5 6 7 8)).
        m2 := MMatrix newFromArrays: #(#(4 2 1 3) #(7 6 8 5)).
        self assert: (m1 - m2) asStructuredArray equals: #(#(-3 0 2 1) #(-2
0 -1 3))

```

## 6.12 用随机数填充矩阵

神经网络的初始状态大多是随机的。因此，我们需要一种随机初始化矩阵的方法。考虑这种方法:

```
MMatrix>>random
         "Fill the matrix with random numbers"
         ^ self random: Random new

```

为初始化提供一个随机发生器可能是方便的:

```
MMatrix>>random: randomNumberGenerator
         "Fill the matrix with random numbers. Takes a random number
              generator as argument"
         self fromContents: ((1 to: nbRows * nbColumns) collect: [ :vv |
              randomNumberGenerator next ])

```

执行表达式`(MMatrixnewRows: 4columns: 5)random`说明了它的用法:

```
(0.2073 0.7154 0.3008 0.06 0.0865
0.3493 0.6396 0.7285 0.4873 0.1947
0.7951 0.3034 0.6066 0.8358 0.1445
0.5454 0.2504 0.2012 0.9086 0.5719 )

```

## 6.13 矩阵值求和

包含在矩阵中的值可以被求和。这对于在训练神经网络时评估成本函数很有用:

```
MMatrix>>sum
        "Return the sum of the matrix values"
        | sum |
        sum := 0.
        1 to: nbRows do: [ :y |
             1 to: nbColumns do: [ :x |
                  sum := sum + (self at: y @ x)
              ]
        ].
        ^ sum

```

测试中说明了`sum`的使用:

```
MMatrixTest>>testSum
        | m |
        m := MMatrix newFromArrays: #(#(1 2 3 4) #(5 6 7 8)).
        self assert: m sum equals: (1 to: 8) sum

```

## 6.14 转置矩阵

转置矩阵是一种操作，包括沿矩阵的对角线翻转矩阵。我们可以将操作定义如下:

```
MMatrix>>transposed
        "Transpose the matrix"
        | result |
        result := MMatrix newRows: nbColumns columns: nbRows.
        1 to: nbRows do: [ :row |
             1 to: nbColumns do: [ :column |
                  result at: column @ row put: (self at: row @ column)
             ]
        ].
        ^ result

```

以下测试说明了`transposed`方法的行为:

```
MMatrixTest>>testTransposedOnMatrix
         | m expectedResult |
         m := MMatrix newFromArrays: #(#(1 2 3 4) #(5 6 7 8)).
         expectedResult := MMatrix newFromArrays: #(#(1 5) #(2 6) #(3 7) #(4 8)).
         self assert: m transposed asStructuredArray equals: expectedResult
              asStructuredArray

```

转置一个向量会产生一个单行矩阵，如下面的测试方法所示:

```
MMatrixTest>>testTransposedOnVector
         | m |
         m := MMatrix newFromVector: #(1 2 3).
         self assert: m transposed asStructuredArray equals: #(#(1 2 3))

```

## 6.15 示例

我们可以在一个简单的反向传播实现中说明矩阵的使用。以下脚本创建了两组随机值，并训练神经网络将输入值映射到输出值。它说明了向前和向后传播的“本质”:

```
n := 8\.               "Number of examples"
din := 10\.            "Number of input values"
h := 20\.              "Size of the hidden layer"
dout := 5\.            "Number of output values"

r := Random seed: 42.
x := (MMatrix newRows: n columns: din) random: r.
y := (MMatrix newRows: n columns: dout) random: r.
w1 := (MMatrix newRows: din columns: h) random: r.
w2 := (MMatrix newRows: h columns: dout) random: r.

learningRate := 1e-6.
losses := OrderedCollection new.
1500 timesRepeat: [
       hh := x +* w1.
       hrelu := hh collect: [ :v | v max: 0 ].
       ypred := hrelu +* w2.
       "Compute and print loss"
       loss := ((ypred - y) collect: [:vv | vv * vv ]) sum.
       losses add: loss.

       "Backprop to compute gradients of w2 and w2 with respect to loss"
       gradYPred := (ypred - y) * 2.0.
       gradW2 := hrelu transposed +* gradYPred.
       gradHRelu := gradYPred +* w2 transposed.
       gradH := gradHRelu collect: [ :v | v max: 0 ].
       gradW1 := x transposed +* gradH.

       w1 := w1 - (gradW1 * learningRate).
       w2 := w2 - (gradW2 * learningRate)
].

g := RTGrapher new.
d := RTData new.
d noDot; connectColor: Color blue.
d points: losses.
d y: #yourself.
g add: d.
g axisX title: 'Epoch'.
g axisY title: 'Error'.
g

```

脚本的最后一部分使用`RTGrapher`来显示损失值随时期的演变(见图 [6-1](#Fig1) )。

![../images/484203_1_En_6_Chapter/484203_1_En_6_Fig1_HTML.jpg](../images/484203_1_En_6_Chapter/484203_1_En_6_Fig1_HTML.jpg)

图 6-1

反向传播的一个简单实现

## 6.16 我们在本章中看到了什么？

本章涵盖了以下主题:

*   最小 C 库的定义。神经网络和深度学习通常采用矩阵来执行计算。

*   *`MMatrix`*类的定义。*这个类模拟了矩阵的数学概念。注意，我们设计了我们的类来为神经网络提供相关的操作。这绝不是一个确定的通用矩阵实现。*

 *这是一个密集的章节。然而，我们创建的矩阵库将在下一章中大大简化我们神经网络的修订版。

构建神经网络的现代库使用矩阵来执行它们的数值计算。然而，传统上使用 GPU 而不是 CPU，正如我们在这里所做的。我们可以使用 CUDA 或 OpenCL 在 GPU 上执行矩阵运算。然而，这将大大增加代码量。这就是为什么我们把自己局限于由 CPU 执行的计算。

在下一章中，我们将重写神经网络实现以使用矩阵。*