# 16.迷你游戏

本章构建了一个受任天堂的马里奥兄弟启发的小视频游戏。我们的游戏版本是真实的马里奥兄弟游戏的简化版。这一章的目的是提供一个坚实和现实的基础，在此基础上我们可以建立一个智能的人工播放器。本章的目标是*而不是*提供精彩的游戏体验。相反，这个游戏提供了一个具有挑战性的场景来练习前一章中提到的算法。我们的游戏，我们称之为*迷你游戏*，有以下特点:

*   游戏有一个英雄，*马里奥*，位于屏幕中央。

*   马里奥可以通过使用键盘或人工玩家来控制。

*   马里奥可以左右移动和跳跃。

*   地图由砖块和管子组成，马里奥无法通过。

*   地图上布满了怪物，马里奥必须避开它们，否则游戏结束。

*   怪物朝一个方向行走，直到撞到一块砖或一根管子，在这种情况下，行走方向会变成相反的方向。

*   游戏的目标是将马里奥带到地图的最右边。

这个游戏是由一个全球脉冲驱动的，我们称之为*节拍*。一拍代表一个不可分割的时间单位。每一拍，马里奥和怪物可以移动一格。注意，为了简单起见，怪物不能跳。

![../images/484203_1_En_16_Chapter/484203_1_En_16_Fig1_HTML.jpg](../images/484203_1_En_16_Chapter/484203_1_En_16_Fig1_HTML.jpg)

图 16-1

迷你游戏

图 [16-1](#Fig1) 是一个游戏的截图。图形方面被缩减到最小，我们简化了游戏的目标，物理引擎也只是简单勾画了一下。马里奥位于中间，被漆成红色。平台是棕色的，天空是蓝色的，管子是绿色的，怪物是橙色的。该图显示马里奥在一个平台旁边的一根管子上。一个怪物在管子的底部。该图展示了我们对游戏视觉方面的大幅简化。

## 16.1 字符定义

我们通过定义角色来开始游戏的实现。两种角色是迷你世界的一部分:马里奥和马里奥应该避免的怪物。马里奥和怪物们有共同点，由`MNAbstractCharacter`类表示:

```
Object subclass: #MNAbstractCharacter
         instanceVariableNames: 'position jumpNbSteps phase game isFalling
              isJumping'
         classVariableNames: ''
         package: 'MiniMario'

```

`MNAbstractCharacter`类有以下变量:

*   `position`指的是表示角色在地图中所处位置的点。

*   `jumpNbSteps`统计角色跳跃时出现的节拍数。这是在没有物理引擎的情况下表达跳跃的必要信息。

*   `phase`表示跳跃的阶段。相位可以是`#goingUp`或`#goingDown`。

*   `game`指的是游戏，`MNWorld`类的一个实例，我们后面会定义。

*   `isFalling`是一个布尔值，表示字符是否正在下落。

*   `isJumping`是一个布尔值，表示角色是否在跳跃。

当它第一次被创建时，角色没有跳跃或下落，而是被设置到`0 @ 0`位置。因此，我们将`initialize`方法定义如下:

```
MNAbstractCharacter>>initialize
           super initialize.
           isJumping := false.
           position := 0 @ 0.
           isFalling := true.

```

如前所述，迷你游戏是由全球节拍驱动的。我们将`beat`方法定义如下:

```
MNAbstractCharacter>>beat
           "Execute a unit of behavior"
           isJumping ifTrue: [
                  phase == #goingUp ifTrue: [ self translateBy: 0 @ -1 ].
                  phase == #doingDown ifTrue: [ self translateBy: 0 @ 1 ].
                  jumpNbSteps := jumpNbSteps + 1.
                  jumpNbSteps = 5 ifTrue: [ phase := #doingDown ].
                  jumpNbSteps = 10 ifTrue: [ isJumping := false ] ].

           isJumping ifFalse: [
                  self isThereAPlatformBelowMe
                        ifTrue: [
                              self setAsFalling.
                              self translateBy: 0 @ 1 ]
                        ifFalse: [ self setAsNotFalling ] ].

```

`beat`方法执行角色的一个行为单元。如果角色在跳跃，`phase`表示垂直平移的方向:角色向上或向下。一跳的时间是`10`，方向在第五拍改变。如果角色没有跳跃，如果它不在一个平台之上，它被设置为下落。

`isThereAPlatformBelowMe`辅助方法对于检查角色下面是否有平台很有用。这种方法对于决定角色是否必须倒下很有用。其定义如下:

```
MNAbstractCharacter>>isThereAPlatformBelowMe
           "Return true if there is no platform below the character"
           ^ (game getCellAt: self position + (0 @ 1)) = 0

```

如果角色下面没有平台，也不是跳跃，则标记为坠落:

```
MNAbstractCharacter>>setAsFalling
           "Set the character as falling"
           isFalling := true

```

如果角色下面有一个平台，那么它不是在下降:

```
MNAbstractCharacter>>setAsNotFalling
           "Set the character as not falling"
           isFalling := false

```

一个角色生活在一个世界中，这个世界是使用以下内容设置的:

```
MNAbstractCharacter>>game: aWorldGame
           "Set the world in which I live"
           game := aWorldGame

```

使用以下方法定义跳跃动作:

```
MNAbstractCharacter>>jump
           "Make the character jump"
           "Do nothing if it is jumping or falling"
           isJumping ifTrue: [ ^ self ].
           isFalling ifTrue: [ ^ self ].
           isJumping := true.
           phase := #goingUp.

```

如果角色正在下落或者已经在跳跃，那么就没什么可做的了。跳跃时，相位设置为`#goingUp`，不记录跳跃步长(`jumpNbSteps:= 0`)。

我们将向左移动的动作定义如下:

```
MNAbstractCharacter>>moveLeft
           "Make the character move left"
           self translateBy: -1 @ 0

```

类似地，向右移动的动作定义如下:

```
MNAbstractCharacter>>moveRight
           "Make the character move right"
           self translateBy: 1 @ 0

```

角色的位置由游戏本身使用。因此，我们定义了一个将由`MNWorld`类使用的访问器，它将在后面定义:

```
MNAbstractCharacter>>position
           "Return the position of the character"
           ^ position

```

一个字符可以被平移给定的距离，表示为一个点:

```
MNAbstractCharacter>>translateBy: aDeltaPosition
           "Translate the character by a delta, if possible"
            (self canGoToward: aDeltaPosition) ifFalse: [ ^ self ].
           position := position + aDeltaPosition

```

请注意，只有在该方向有空间的情况下，才会进行平移。

`canGoToward:`实用程序方法对于检查角色是否可以向特定方向移动很有用:

```
MNAbstractCharacter>>canGoToward: aDeltaPosition
           "Return true if the character can go toward a direction"
           ^ (game getCellAt: position + aDeltaPosition) = 0

```

构建时，世界游戏使用以下方法设置角色的位置:

```
MNAbstractCharacter>>translateTo: aPosition
           "Set a position of the character"
           position := aPosition

```

我们现在可以定义一些具体的字符实现。

## 16.2 建模马里奥

我们将`MNMario`类定义如下:

```
MNAbstractCharacter subclass: #MNMario
           instanceVariableNames: ''
           classVariableNames: ''
           package: 'MiniMario'

```

我们覆盖`beat`方法如下:

```
MNMario>>beat
           super beat.
           "The game ends if Mario reaches the
           right-most position of the game"
           self position x = (game size x - 1) ifTrue: [ game gameOver ].

           "The game also ends if Mario bumps into a monster"
           game monsters do: [ :m |
                     (m position = self position) ifTrue: [ game gameOver ] ]

```

有两种可能的方法来结束游戏。要么是(I)马里奥到达了地图的最右端，要么是(ii)马里奥撞上了一个怪物。如你所见，马里奥的定义相对较短，因为大部分逻辑是在`MNAbstractCharacter`中定义的。

## 16.3 模拟一个人造马里奥玩家

在定义了由人类控制的马里奥玩家之后，我们可以设置由神经网络来控制马里奥。我们将`MNAIMario`类定义如下:

```
MNMario subclass: #MNAIMario
           instanceVariableNames: 'network'
           classVariableNames: ''
           package: 'MiniMario'

```

`MNAIMario`定义的唯一变量是`network`，指的是一个神经网络，充当马里奥的大脑。这个网络是由 NEAT 算法提供的。`beat`方法现在应该充分利用网络:

```
MNAIMario>>beat
          | actionToPerform |
          super beat.
          actionToPerform := network predict: game whatMarioSees.
          actionToPerform = 0 ifTrue: [ self moveLeft ].
          actionToPerform = 1 ifTrue: [ self moveRight ].
          actionToPerform = 2 ifTrue: [ self jump ].

```

马里奥有三种可能的动作:向左移动、向右移动或跳跃。因此，神经网络必须有三个输出。我们使用前一章看到的`predict:`方法来决定最合适的行动。网络的输入是马里奥所看到的表象。

可以使用以下方式向模拟玩家提供网络:

```
MNAIMario>>network: aNeuralNetwork
          "Set the neural network meant to be used by Mario"
          network := aNeuralNetwork

```

## 16.4 怪物建模

在我们的游戏中，怪物被塑造成一个角色。我们将`MNMonster`类定义为`MNAbstractCharacter`的子类:

```
MNAbstractCharacter subclass: #MNMonster
           instanceVariableNames: 'movingLeft pauseCounter'
           classVariableNames: ''
           package: 'MiniMario'

```

该类定义了两个变量:

*   `movingLeft`表示怪物向左移动还是向右移动。

*   减缓怪物的行为。

基于这两个变量的定义，一个 monster 被初始化如下:

```
MNMonster>>initialize
           super initialize.
           movingLeft := true.
           pauseCounter := 0

```

创建时，一个怪物向左移动。当它撞到一个平台时，然后怪物转向相反的方向，向右移动，直到它再次撞到什么东西。该行为在`MNMonster>>beat`方法中实现:

```
MNMonster>>beat
           "A monster can go to the left or to the right"
           super beat.
           pauseCounter := pauseCounter + 1.
           pauseCounter < 10 ifTrue: [ ^ self ].
           pauseCounter := 0.

           movingLeft
                   ifTrue: [ (self canGoToward: -1 @ 0) ifFalse: [ movingLeft :=
                        false ] ]
                   ifFalse: [ (self canGoToward: 1 @ 0) ifFalse: [ movingLeft :=
                        true ] ].
           movingLeft ifTrue: [ self moveLeft ] ifFalse: [ self moveRight ].

```

我们使用`pauseCounter`变量将怪物行为的执行速度降低 10 倍。这个因素有助于使迷你游戏可以被人类玩。

## 16.5 模拟极简世界

在定义了角色之后，我们现在需要定义世界。这个类相当长，但也相当简单。我们将`MNWorld`类定义如下:

```
Object subclass: #MNWorld
         instanceVariableNames: 'map mario view monsters isGameRunning
              random'
         classVariableNames: ''
         package: 'MiniMario'

```

该类定义了这些变量:

*   `map`是一个大型 2D 数组，包含描述地图的数值。特别地，值`0`表示空单元，`1`表示砖块，`2`表示管。

*   `mario`指马里奥对象。

*   `view`是将要添加元素的 Roassal 视图。

*   `monsters`是游戏中生活的怪物的集合。

*   `isGameRunning`是一个布尔值，表示游戏是否正在运行。

*   `random`是一个随机数生成器，用于生成地图。

使用以下内容初始化世界:

```
MNWorld>>initialize
           super initialize.
           self initializeMario.
           self seed: 42.
           self initializeMonsters.
           isGameRunning := true

```

世界初始化首先初始化马里奥，用`42`的任意种子生成地图。随后，怪物被初始化，游戏被设置为可运行。世界地图是一幅长的水平地图，其大小由下式给出:

```
MNWorld>>size
           "Return the size of the map"
           ^ 300 @ 10

```

地图的大小以像元的数量表示。随机数发生器由`seed:`方法触发，定义如下:

```
MNWorld>>seed: aNumber
            "Create a random number generator with a particular seed"
            random := Random seed: aNumber.
            self generateMap.

```

`generateMap`方法生成世界地图，我们将在后面看到。马里奥是使用以下内容创建的:

```
MNWorld>>initializeMario
            "Create a Mario object"
            mario := MNMario new.
            mario translateTo: 2 @ 2.
            mario game: self.

```

马里奥的初始位置在左上角，位置`2 @ 2`。怪物创建如下:

```
MNWorld>>initializeMonsters
            "Add a number of monsters"
             monsters := OrderedCollection new.
             10 timesRepeat: [
                    | m |
                    m := MNMonster new translateTo: (random nextInt: self size x) @ 2.
                    self addMonster: m ]

```

怪物随机出现在地图中。马里奥和怪物们有一个初始位置，其组成部分为`2`。在游戏开始时，怪物会掉落，因为它们最初位于地图的上部，而平台在下面。使用以下代码将一个怪物添加到世界中:

```
MNWorld>>addMonster: aMonster
            "Add a monster to the world"
            monsters add: aMonster.
            aMonster game: self.

```

使用此方法将砖块添加到世界中:

```
MNWorld>>addBrick: position
            "Add a brick to a position"
            (self isInMap: position)
                   ifTrue: [ self cellAt: position put: 1 ]

```

一个平台就是五块排成一行的砖块。`addPlatform:`方法向世界添加一个位于特定位置的平台:

```
MNWorld>>addPlatform: position
            "A platform is horizontal and made of 5 bricks"
            -2 to: 2 do: [ :i |
                  self addBrick: position + (i @ 0) ]

```

在马里奥的世界里，砖块和管子是马里奥无法穿过的两种元素，迫使他在它们周围跳跃。我们将管定义如下:

```
MNWorld>>addTube: positionX
            "Add a tube at a given position in the map"
            | indexY |
            indexY := self size y - 1.
            3 timesRepeat: [
                 self addTubeCell: positionX @ indexY.
                 indexY := indexY - 1 ].

```

使用以下方法添加试管单元:

```
MNWorld>>addTubeCell: position
            "Add a cell representing a tube at a given position"
            (self isInMap: position)
                   ifTrue: [ self cellAt: position put: 2 ]

```

世界上使用的整体地图是使用`generateMap`方法定义的:

```
MNWorld>>generateMap
            "Randomly generate the map used in the world"
            map := Array2D rows: self size y columns: self size x element: 0.
            1 to: self size x do: [ :x | self addBrick: (x @ self size y);
                 addBrick: (x @ 1) ].
            1 to: self size y do: [ :y | self addBrick: (1 @ y); addBrick: (
                 self size x @ y) ].

            "The map has 80 platforms..."
            80 timesRepeat: [
                  self addPlatform: (random nextInt: self size x) @ (random
                       nextInt: self size y) ].

            "... and 30 tubes"
            30 timesRepeat: [ self addTube: (random nextInt: self size x) ]

```

`generateMap`方法依赖于随机数发生器。每一代人都会创造一个新世界。首先，`map`内容用值`0`填充，代表一个空单元格。地图四周用砖块围起来，防止马里奥到达没有定义地图的地方。然后我们生成 80 个平台和 30 个管道。请注意，这种构建可玩世界的方式非常简单。例如，代码不验证出口是否可达。在实践中，似乎只有少数地图不能玩到最后。

可以使用以下方法检索单元格的值:

```
MNWorld>>getCellAt: aPoint
            "Return the value of a particular cell"
            ^ map at: aPoint y at: aPoint x

```

```
MNWorld>>cellAt: aPoint put: value
            "Set the value of a particular cell"
            ^ map at: aPoint y at: aPoint x put: value

```

这与角色是否应该坠落有关，取决于它是否站在平台上。确定位置是否在地图内有助于防止访问地图外的像元:

```
MNWorld>>isInMap: position
         "Return true if the position is within the map"
         ^ (1 @ 1 extent: self size) containsPoint: position

```

我们现在可以专注于游戏本身的逻辑。首先要关注的是节拍。游戏的全局节拍定义如下:

```
MNWorld>>beat
            "The world beat performs a beat on each monster and on Mario"
            isGameRunning ifFalse: [ ^ self ].
            monsters do: #beat.
            mario beat.
            self refreshView

```

如果马里奥碰到最右边的墙或者撞到怪物，游戏就结束了。`gameOver`方法显示一条消息并结束游戏:

```
MNWorld>>gameOver
            "End the game"
            isGameRunning := false.
            view ifNil: [ ^ self ].
            view add: ((RTLabel new color: Color red; text: 'GAME OVER')
            element translateBy: 0 @ -100)

```

一个新的马里奥角色可以通过以下方式插入到世界中:

```
MNWorld>>mario: aMario
            "Set Mario in the game"
            mario := aMario.
            mario game: self.
            mario translateTo: 2 @ 2

```

当加入游戏时，一个马里奥角色被移动到地图的左上角，坐标为`2@2`。马里奥角色通过以下方法从世界中访问:

```
MNWorld>>mario
            "Return the Mario character"
            ^ mario

```

正如我们将看到的，访问马里奥在健身功能中是有用的，因为我们可以在完成特定次数的节拍后获得马里奥的位置。

怪物必须可以从外面接近，因为马里奥角色需要知道他什么时候碰到了怪物。怪物可以通过以下方式获得:

```
MNWorld>>monsters
            "Return the list of monsters living in the world"
            ^ monsters

```

最后一个方法总结了游戏模型的定义。我们现在可以专注于游戏的视觉方面。

## 16.6 构建游戏的视觉效果

游戏的模型现在已经准备好了，我们“简单地”需要将它连接到 Roassal 来创建一个可视化的表示。地图是一个包含单元的长水平矩阵。当呈现给人类或人造玩家时，只有一小部分游戏是可见的。可视部分是可视游戏单元的正方形部分。该部分的大小由以下因素决定:

```
MNWorld>>windowSize
            "Number of pixels of a window frame side"
            ^ 11

```

因此，播放时会显示一个正方形的`11 ∗ 11`单元格。总共有 121 个细胞可见。当我们调优 NEAT 算法时，这个单元数很重要。游戏的用户界面是使用`generateUI`方法构建的:

```
MNWorld>>generateUI
            "Build the game user interface"
            | e upperBounds lowerBounds cellSizeInPixel |
            "Size of each cell"
            cellSizeInPixel := 25.

            "Create the visual representation of cells"
            view := RTView new.
            upperBounds := self windowSize // 2.
            lowerBounds := upperBounds negated.
            lowerBounds to: upperBounds do: [ :x |
            lowerBounds to: upperBounds do: [ :y |
                    e := RTBox new size: cellSizeInPixel + 1; elementOn: x @ y.
                    view add: e.
                    e translateTo: (x @ y) ∗ cellSizeInPixel ] ].

       "Define the actions to be taken when keys are pressed"
       view when: TRKeyDown do: [ :evt |
               "Key D"
               evt keyValue = 100 ifTrue: [ mario moveRight ].

               "Key A"
               evt keyValue = 97 ifTrue: [ mario moveLeft ].

               "Key W"
               evt keyValue = 119 ifTrue: [ mario jump ] ].

       "A beat is performed at each update of the UI"
       view addAnimation: (RTActiveAnimation new intervalInMilliseconds:
               30; blockToExecute: [ self beat ]).
       self refreshView.

```

`generateUI`方法从定义`cellSizeInPixel`变量开始，该变量对应于每个单元格的可视表示的大小。减小`cellSizeInPixel`值会使窗口变小。为可见地图部分的每个单元格创建一个 Roassal 元素。

三个动作被绑定到通过按下`D`、`A`和`W`键进行的击键。每 30 毫秒执行一次游戏节拍，并伴随着更新 UI。`refreshView`方法实现了这个行为:

```
MNWorld>>refreshView
            "Research the UI"
            | p t color |
            view isNil ifTrue: [ ^ self ].
            isGameRunning ifFalse: [ ^ self ].

            p := mario position.
            view elements doWithIndex: [ :e :index |
                    t := p + e model.
                    "Empty cells are blue
                    Platform cells are brown
                    Tube cells are green
                    Cells outside the map are black"
                   (self isInMap: t)
                           ifTrue: [
                                 (self getCellAt: t) = 0 ifTrue: [ color := Color blue ].
                                 (self getCellAt: t) = 1 ifTrue: [ color := Color brown ].
                                 (self getCellAt: t) = 2 ifTrue: [ color := Color green ] ]
                           ifFalse: [ color := Color black ].

                      "Mario is red"
                      e model = (0 @ 0) ifTrue: [ color := Color red ].
                      e trachelShape color: color ].

            monsters do: [ :m |
                      t := m position - p.
                      "Only monsters that are within the window frame are rendered"
                      t x abs < self windowSize ifTrue: [
                           | cell |
                           cell := view elements elementFromModel: t.
                           "Monsters are orange"
                           cell notNil ifTrue: [ cell trachelShape color: Color orange ] ].
            ].
            view signalUpdate.

```

方法定义了各种图形元素的颜色编码。使用以下方法打开用户界面:

```
MNWorld>>open
            "Open the UI"
            self inspect.
            self generateUI.
            ^ view open

```

`open`方法显示游戏的 UI，并在游戏本身上打开一个检查器。为了调整游戏，这很方便。NEAT 算法需要为神经工作提供一些输入。这些输入对应于可见单元格的值。为此，我们定义了以下方法:

```
MNWorld>>whatMarioSees
            "Return the values of the visible cells"
            | result p t upperBounds lowerBounds |
            result := OrderedCollection new.
            p := mario position.

            upperBounds := self windowSize // 2.
            lowerBounds := upperBounds negated.
            lowerBounds to: upperBounds do: [ :x |
                    lowerBounds to: upperBounds do: [ :y |
                            t := p + (x @ y).
                             (self isInMap: t)
                            ifTrue: [ result add: (self getCellAt: t) ]
                            ifFalse: [ result add: 1 ] ] ].
            ^ result

```

`whatMarioSees`方法返回一个由`0`、`1`和`2`组成的集合，对应于马里奥所看到的内容。当我们使用 NEAT 时，这些值将被输入神经网络。

我们现在将定义一个方法来提供游戏的概述。请注意，这个方法不是玩游戏或训练 AI 所必需的。考虑以下方法:

```
MNWorld>>showCompleteMap
            "Show the complete map"
            | v cellSizeInPixel color e |
            v := RTView new.
            v @ RTDraggableView.
            cellSizeInPixel := 4.
            1 to: self size x do: [ :x |
                  1 to: self size y do: [ :y |
                        color := Color black.
                         (self getCellAt: x @ y) = 0 ifTrue: [ color := Color blue ].
                         (self getCellAt: x @ y) = 1 ifTrue: [ color := Color brown ].
                         (self getCellAt: x @ y) = 2 ifTrue: [ color := Color green ].

                        e := RTBox new size: cellSizeInPixel + 1; color: color;
                              elementOn: x @ y.
                        v add: e.
                        e translateTo: (x @ y) ∗ cellSizeInPixel ] ].
            v open

```

`showCompleteMap`方法显示了级别的概况。

## 16.7 跑步迷你吧

游戏现在完成了。人类可以通过简单地执行这个表达式来玩它:

```
MNWorld new open

```

使用`A`和`D`键分别向左和向右移动来控制马里奥。`W`键让马里奥跳起来。感谢我们生成关卡的方式，您可以使用`seed:`方法轻松创建新的关卡。例如:

```
MNWorld new seed: 7; open

```

提供给`seed:`的数字与特定级别相关联。默认地图使用以下表达式显示:

```
MNWorld new showCompleteMap

```

当提供种子时，表达式变为:

```
MNWorld new seed: 7; showCompleteMap

```

两张图的结果如图 [16-2](#Fig2) 所示。

![../images/484203_1_En_16_Chapter/484203_1_En_16_Fig2_HTML.jpg](../images/484203_1_En_16_Chapter/484203_1_En_16_Fig2_HTML.jpg)

图 16-2

为 MiniMario 生成的地图示例

## 16.8 简洁和简约

MiniMario 的 UI 是由彩色单元格组成的地图的方形部分。正如在`MNWorld>>windowSize`方法中定义的，可视帧的每一边有 11 个单元格。显示的单元格数是 11 乘以 11，即 121。马里奥由三个不同的命令控制——向左移动、向右移动和跳跃。这些信息对网络应该是什么样子给出了一些提示:121 个输入神经元和 3 个输出神经元。考虑以下脚本:

```
neat := NEAT new.
         neat numberOfInputs: 121.
         neat numberOfOutputs: 3.
         neat populationSize: 200.

         neat fitness: [ :ind |
                w := MNWorld new.
                w mario: (MNAIMario new network: ind).
                450 timesRepeat: [ w beat. ].
                w mario position x ].

         neat numberOfGenerations: 160.
         neat run.

```

该算法维持 200 个个体的群体，每个个体代表一个神经网络。体能分数是以马里奥离关卡终点的距离来计算的。提供给`fitness:`的程序块具有以下指令序列:

1.  创造一个极简世界。

2.  将马里奥定义为一个以个人为代表的网络的`MNAIMario`。

3.  表演 450 个世界节拍。

4.  返回马里奥的`x`位置。

因此，两个单独的脚本(神经网络)可以很容易地进行比较:可以将马里奥带到更右边的那个被认为更好。前面的脚本需要时间:在双核英特尔酷睿 i5(1.6 GHz，8GB)上大约需要 13 分钟。

![../images/484203_1_En_16_Chapter/484203_1_En_16_Fig3_HTML.jpg](../images/484203_1_En_16_Chapter/484203_1_En_16_Fig3_HTML.jpg)

图 16-3

极简和简洁

图 [16-3](#Fig3) 显示了进化的结果。

`neat result`表达式返回进化的最佳个体，即能把马里奥带到地图最右边的那个。MiniMario 可以使用人工智能播放器运行，脚本如下:

```
...
w := MNWorld new.
w mario: (MNAIMario new network: neat result).
w open

```

正如你所看到的，我们定义的 AI 可以在避开怪物的同时带领马里奥到达关卡的终点。因此，我们制造了一个人造播放器！

## 16.9 我们在本章中看到了什么？

这一章不直接涉及人工智能的话题。然而，它提供了著名的 Mario Bros 游戏的简化版本的一个小实现。特别是，这一章包括:

*   一个小游戏的设计被调整为插入 NEAT 算法。

*   一个`MNAIMario`级的人造玩家的骨架。

*   NEAT 在极小极大上的应用。

NEAT 可以进化出一个神经网络来完成 MiniMario 游戏。这因此结束了这本书的第三和最后一部分。